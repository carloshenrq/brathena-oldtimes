//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Buying Shop Licenses                   |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.0/1.1                                |
// |--------------------------------------------------------|
// | Descrição:	Buying Shop Licenses.                       |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [YuMs]		                    |
// | 1.1 Script Revisado [Mark]			                    |
// | - Anotações		   			    					|
// \________________________________________________________/

// Black Marketeer (Buy Licenses - Non-Merchant Classes)
//============================================================
que_job01,68,84,1	script	Black Marketeer#Buying	881,{
	if(getskilllv("ALL_BUYING_STORE") == 1) {
		mes "[Sr. Jass]";
		mes "Ei, você já fez um contrato com Hugh.";
		mes "Eu não tenho nenhum negócio com você.";
		close;
	}else{
		mes "[Sr. Jass]";
		mes "Você deve precisar muito de alguma coisa para vir me procurar.";
		mes "O que você precisa?";
		next;
		switch(select("Lincença de Grande Comprador de Lojas:Quem é você?:Nada, nada!")) {
		case 1:
			mes "[Sr. Jass]";
			mes "Eu sabia!";
			mes "Claro, eu posso fazer isso por você.";
			mes "Mine apenas se parece com os autênticos comerciantes que começa a partir desse canalha Sr. Hugh!";
			next;
			mes "[Sr. Jass]";
			mes "E a minha licença é melhor 'porque você não precisa' aprender todas as habilidades.";
			mes "Quantas você quer?";
			next;
			mes "[Sr. Jass]";
			mes "Só para você saber, eu só posso fazê-las em pequenas quantidades, até 10 de uma vez.";
			mes "Vai custar 500 zeny para cada um.";
			next;
			while(1) {
				input .@input;
				mes "[Sr. Jass]";
				if(.@input == 0) {
					mes "Você não precisa dessas licenças?";
					close;
				}
				else if(.@input > 10) {
					mes "Eu só posso fazer até 10 de cada vez, você sabe.";
					next;
				}
				else {
					mes "Vai custar "+(.@input*500)+" zeny.";
					if(Zeny < (.@input*500)) {
						mes "mas você não tem dinheiro suficiente.";
						next;
						mes "[Sr. Jass]";
						mes "Você não sabe o básico de negócios? Tudo tem um preço.";
						mes "Se você quer algo, você tem que pagar por isso.";
					}else{
						mes "Ha... Ha ha ha!";
						mes "Sr. Hugh, Eu vou assumir a sua licença comercial. Você vai ver!";
						mes "*Risos Risos*";
						getitem 12548,.@input;	// Shabby_Purchase_Street
						set Zeny,Zeny-(.@input*500);
					}
					close;
				}
			}

		case 2:
			mes "[Sr. Jass]";
			mes "Deixei minha cidade natal há muito tempo.";
			mes "Faz sentido perguntar quem eu sou, porque tudo o que eu tive deixado para trás até agora foi meu ódio";
			next;
			mes "[Sr. Jass]";
			mes "...";
			mes "Hugh é um comerciante corrupto sem nenhum senso de ética nos negócios.";
			mes "Meu único objetivo na vida é destruir Hugh.";
			next;
			mes "[Sr. Jass]";
			mes "Ah, eu bebi demais... (*Soluço*)";
			mes "Isso é só o álcool a falar.";
			mes "Por favor, esqueça tudo o que eu disse.";
			close;
		case 3:
			mes "[Sr. Jass]";
			mes "Certo, certo! Você não precisa gritar.";
			mes "Me deixe em paz se você não tem negócio comigo.";
			close;
		}
	}
}

// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================
alberta_in,58,52,4	script	Purchasing Team#Buying	59,{
	if(BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if(getskilllv("ALL_BUYING_STORE") == 1) {
			mes "[Sr. Hugh]";
			mes "Eu sou Hugh da Equipe de Compras.";
			mes "Como posso ajudá-lo hoje?";
			next;
			// Tradução adaptada para menu
			if(select("Comprar Licença para Loja de Compras:Sair") == 2) {
				mes "[Sr. Hugh]";
				mes "Por favor, não hesite em perguntar-me, se você precisar de alguma Licença para Loja de Compras em Massa";
				mes "Volte sempre~";
				close;
			}
			mes "[Sr. Hugh]";
			mes "São 200 zeny para cada Licença para Loja de Compras em Massa, e você pode comprar até 50 por vez.";
			mes "Quantas licenças você precisa?";
			next;
			while(1) {
				input .@input;
				mes "[Sr. Hugh]";
				if(.@input == 0) {
					mes "Você cancelou o negócio.";
					mes "Tenha um bom dia.";
					close;
				}
				if(.@input > 50) {
					mes "Por favor insira um valor menor que 51";
					next;
				}
				else{
					mes "Vai custar "+(.@input*200)+" zeny para "+.@input+" licenças.";
					if(Zeny < (.@input*200)) {
						mes "mas você parece não ter dinheiro suficiente.";
					}else{
						mes "Obrigado pelo seu patrocínio.";
						getitem 6377,.@input;	// Buy_Stall_Permit
						set Zeny,Zeny-(.@input*200);
					}
					close;
				}
			}
		}else{
			mes "[Sr. Hugh]";
			mes "Eu sou Hugh da Equipe de Compras do Clã dos Mercadores de Alberta.";
			mes "Você está...";
			next;
			mes "[Sr. Hugh]";
			mes "Você sabia? Nosso Clã tem emitido uma licença que permiti que os indivíduos possa comprar mercadorias de outros.";
			next;
			select("Eu nunca tive problemas comprando itens...");
			mes "[Sr. Hugh]";
			mes "Você está certo, mas pense comigo:";
			mes "Você não teve dificuldade de comprar em massa?";
			mes "Você tem que encontrar e conversar com todo mundo que tem um item que você deseja.";
			mes "Consideravelmente inconveniente, não é?";
			next;
			mes "[Sr. Hugh]";
			mes "Uma vez que a compra em massa é uma questão importante para nós, comerciantes,";
			mes "Eu tenho proposto um plano inovador para o nosso Clã, baseado em meus 10 anos de experiência em fazer compras.";
			next;
			mes "[Sr. Hugh]";
			mes "'Vamos Abrir Individualmente";
			mes "Uma Loja de Muitos Compradores!'";
			mes "Esse é o título da minha proposta.";
			mes "Você vai ver, quando você ler isso...";
			next;
			mes "[Sr. Hugh]";
			mes "...";
			mes "...(Sr. Hugh falou-me muito, nos mínimos detalhes.)";
			next;
			select("Certo, onde quer chegar?");
			mes "[Sr. Hugh]";
			mes "Ah, sim. Em resumo,";
			mes "você pode comprar alguns itens em massa através de Lojas.";
			next;
			mes "[Sr. Hugh]";
			mes "Para abrir uma Loja de Compra em Massa, você precisa de uma licença emitida pelo Clã Mercador.";
			next;
			mes "[Sr. Hugh]";
			mes "Você precisa dela, cada vez que for abrir uma loja. Nós estamos esperando um aumento significativo nos lucros através deste novo tipo de licenciamento.";
			next;
			mes "[Sr. Hugh]";
			mes "Esse é o ponto da minha proposta!";
			mes "Nosso presidente ficou muito feliz em saber que nós vamos fazer muito dinheiro!";
			next;
			select("Por favor, vá direto ao ponto!");
			mes "[Sr. Hugh]";
			mes "Não seja tão impaciente, certo?";
			mes "Concluíndo, podemos deixá-lo abrir uma Loja de Compra em Massa, isso se você já aprendeu a vender.";
			next;
			mes "[Sr. Hugh]";
			mes "Claro, nós cobramos 10,000 zeny uma única vez por uma taxa de inscrição.";
			mes "Você vai precisar da ^4A4AFFLicença para Loja de Compras em Massa^000000 cada vez que você abrir uma loja.";
			next;
			mes "[Sr. Hugh]";
			mes "Diga, gostaria de registrar agora?";
			mes "Se o fizer, eu vou te ensinar como abrir uma Loja de Compras em Massa";
			next;
			// Tradução adaptada para menu
			if(select("Quero abrir uma loja!:Cancelar") == 2) {
				mes "[Sr. Hugh]";
				mes "Cara, isso é decepcionante!";
				mes "Usando esta opção de compra em massa você pode beneficiar o seu negócio em muitas maneiras, mas a decisão é sua.";
				mes "Estou sempre aberto para consulta!";
				close;
			}
			mes "[Sr. Hugh]";
			if(Zeny < 10000) {
				mes "A taxa de inscrição é de um valor único de 10,000 zeny.";
				mes "Por favor, tenha o dinheiro em mãos.";
				close;
			}else{
				mes "Você fez uma boa decisão.";
				mes "Por favor me dê a taxa de inscrição e assine aqui mesmo....";
				next;
				input .@name$;
				mes "[Sr. Hugh]";
				mes .@name$+"....";
				mes "Eu gosto da sua escrita.";
				mes "Ok, você agora está aprovado de abrir uma Loja de Compras em Massa.";
				set Zeny,Zeny-10000;
				getitem 6377,5;	// Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,0;
				next;
				mes "[Sr. Hugh]";
				mes "Atualmente, apenas itens normais ^8C2121EXCETO^000000 equipamentos, algumas poções e itens artesanais podem ser adquiridos em massa, mas esta ainda pode ser de muito benéfico para você, dependendo de como usa-la.";
				next;
				mes "[Sr. Hugh]";
				mes "Ah, e você precisa de pelo menos um dos itens que você deseja comprar em seu inventário porque você tem que mostrá-la a outros através de sua loja.";
				next;
				mes "[Sr. Hugh]";
				mes "Sua habilidade agora deve estar registrada em sua janela de habilidades. Se você não pode vê-la, provavelmente deverá minimizar a sua Lista de Habilidades e checar na Tabela das 3rd Classes";
				next;
				mes "[Sr. Hugh]";
				mes "Eu te dei 5 Licença para Loja de Compras em Massa para o seu experimento.";
				mes "Boas compras!";
				close;
			}
		}
	}else{
		mes "[Sr. Hugh]";
		mes "Eu sou Hugh da Equipe de Compras do Clã dos Mercadores de Alberta.";
		mes "Eu adoraria conversar, mas eu estou muito ocupado no momento.";
		next;
		mes "[Sr. Hugh]";
		mes "Meu tempo é dedicado exclusivamente aos nossos clientes na indústria do comercio.";
		close;
	}
}