//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Cidade de Brasilis                     |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.1/1.1                                |
// |--------------------------------------------------------|
// | Descrição: NPCs da Cidade de Brasilis                  |
// |--------------------------------------------------------|
// | Changelog:                                             |
// | 1.0 Script Traduzido [cleyton]                         |
// | 1.1 Script Otimizado [Architect]						|
// \________________________________________________________/

alberta,247,115,3	script	Brute Sailor#bra2	100,{
	mes "[Marinheiro Bruto]";
	mes "Ei! Você já ouviu falar de Brasilis?";
	mes "É um lugar tão quente quanto os desertos de Morroc e tão denso quanto as florestas de Payon.";
	next;
	mes "[Marinheiro Bruto]";
	mes "Eu tenho um mapa para chegar até lá, se você quiser eu posso levar você até Brasilis.";
	mes "Isso vai custar 10.000 zeny. O que você acha?";
	next;
	if (select("Vamos para Brasilis!:Cancelar.") == 1) {
		if (Zeny > 9999	) {
			mes "[Marinheiro Bruto]";
			mes "Ótimo! Vamos lá!";
			close2;
			set Zeny,Zeny-10000;
			warp "brasilis",314,60;
			end;
		}
		else {
			mes "[Marinheiro Bruto]";
			mes "Eu lamentos, mas você";
			mes "não tem dinheiro suficiente agora.";
			mes "Você precisa de 10.000 zeny";
			mes "para ir até Brasilis.";
			mes "Muito obrigado!";
			close;
		}
	}
	mes "[Marinheiro Bruto]";
	mes "Me avise se você mudar de idéia.";
	close;
}

brasilis,316,57,3	script	Brute Sailor#bra1	100,{
	mes "[Marinheiro Bruto]";
	mes "Meu navio vai voltar para Alberta em breve. Gostaria de voltar também?";
	next;
	if (select("Voltar para Alberta.:Cancelar.") == 1) {
		mes "[Marinheiro Bruto]";
		mes "Tudo certo, vamos indo!";
		close2;
		warp "alberta",244,115;
		end;
	}
	mes "[Marinheiro Bruto]";
	mes "Me avise se você mudar de idéia.";
	close;
}

brasilis,155,165,3	script	Quadro de Aviso#bra1	858,{
	mes ":: Pousada Tucano Feliz ::";
	close;
}

brasilis,195,231,3	script	Quadro de Aviso#bra2	858,{
	mes ":: Mercado Brasilis ::";
	close;
}

brasilis,240,247,3	script	Quadro de Aviso#bra3	858,{
	mes ":: Museu de Arte Brasilis ::";
	close;
}

brasilis,303,309,3	script	Quadro de Aviso#bra4	858,{
	mes ":: Teleférico da Floresta ::";
	mes " ";
	mes "- Não recomendado para quem tem problemas cardíacos! -";
	close;
}

brasilis,137,77,5	script	Ice-Cream Maker	85,{
	mes "[Ice Cream Maker]";
	mes "Come~come~";
	mes "Ice cream is the perfect snack for a hot day~";
	mes "It's just ^3355FF100 Zeny^000000~";
	mes "Ice Cream~";
	mes "Get 'yer Ice Cream!";
	next;
	switch(select("Give me one!:Ice Cream?:Cancel.")) {
	case 1:
		mes "[Ice Cream Maker]";
		mes "Since there are so many people want to get a cool ice cream you can order only 5 at a time.";
		mes "So how many d'ya want?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Ice Cream Maker]";
				mes "None?";
				mes "Fine get outta the way, I have customers to serve.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[Ice Cream Maker]";
				mes "Wow.";
				mes "You ordered too much.";
				mes "If you eat over 5 you might need to fight with a monster in your stomach. Calm down buddy.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Ice Cream Maker]";
			mes "Dood~! You don't have enough money.";
			mes "It's only ^3355FF100 Zeny^000000~ Seriously!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Ice Cream Maker]";
			mes "You seem to have too much stuff.";
			mes "Lighten your pack before buying this.";
			close;
		}
		set zeny,zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[Ice Cream Maker]";
		mes "'Ice cream is...";
		mes "Wait, don't you know";
		mes "what Ice Cream is?";
		mes "What rock have you";
		mes "been living under?";
		next;
		mes "[Ice Cream Maker]";
		mes "I'm not going to even start with how weird that sounds.";
		mes "Anyway, get 'yer Ice Cream right here while it's nice and cold.";
		close;
	case 3:
		mes "[Ice Cream Maker]";
		mes "Don't miss your chance to eat the greatest Ice Cream in all the land~!";
		close;
	}
}