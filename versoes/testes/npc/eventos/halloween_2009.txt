//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: iRO Halloween (2009)                   |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.1/1.0                                |
// |--------------------------------------------------------|
// | Descrição:                                             |
// | iRO Halloween (2009)                                   |
// | Quest for Weird Pumpkin Hat.                           |
// | Exchange treats for buffs.                             |
// | Summon event monsters in towns.                        |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [Wolf]                            |
// | - Anotações		   			   						|
// \________________________________________________________/

// Headgear Quest - Pumpkin Hat
// ============================
prontera,152,192,5	script	Pesquisador de Chapéis de Abóbora	712,{
	if(BaseLevel < 45) {
		mes "[Pesquisador de Chapéis de Abóbora]";
		mes "Xô, eu não quero crianças. Xô! Eu não falo com novatos.";
		next;
		mes "[Pesquisador de Chapéis de Abóbora]";
		mes "Chegue a um nível que você possa lutar com monstros fortes, e depois volte aqui.";
		close;
	}
	mes "[Pesquisador de Chapéis de Abóbora]";
	mes "Você gosta de Torta de Abóbora, né?";
	next;
	mes "[Pesquisador de Chapéis de Abóbora]";
	mes "Eu sou um pesquisador, pesquiso sobre o Chapéu de Abóbora, não quer ouvir minha história?";
	next;
	while(1) {
		switch(select("Ouvir a história.:Perguntar sobre o Chapéu de Abóbora.:Pegar o Chapéu de Abóbora.:Parar de conversar.")) {
			case 1:
				mes "[Pesquisador de Chapéis de Abóbora]";
				mes "Estive estudando sobre a criação desse grande Chapéu de Abóbora.";
				mes "E eu descobri que é um processo muito simples.";
				next;
				mes "[Pesquisador de Chapéis de Abóbora]";
				mes "O processo é simplesmente esse:";
				mes "Se você trouxer ^4a4aff20 Zés-Lanternas^000000, eu posso te mostrar.";
				mes "Não é uma proposta tentadora?";
				next;
				break;
			case 2:
				mes "[Pesquisador de Chapéis de Abóbora]";
				mes "Esse grande Chapéu de Abóbora é uma coisa muito poderosa.";
				next;
				mes "[Pesquisador de Chapéis de Abóbora]";
				mes "A torta de abóbora quando usada, recupera uma grande quantidade de HP & SP da pessoa que a come.";
				next;
				mes "[Pesquisador de Chapéis de Abóbora]";
				mes "Só você trazer ^4a4aff20 Zés-Lanternas^000000s.";
				next;
				break;
			case 3:
				mes "[Pesquisador de Chapéis de Abóbora]";
				mes "Você quer a Torta de Abóbora? Tudo bem, deixe-me contar quantos Zé-Lanternas você me trouxe.";
				next;
				if(countitem(1062) < 20) {
					mes "[Pesquisador de Chapéis de Abóbora]";
					mes "Eu preciso de ^4a4affZé-Lanternas^000000.";
					mes "Eu não sou um alquimista ou um arquimago para criar algo sem nada.";
					next;
					mes "[Pesquisador de Chapéis de Abóbora]";
					mes "Ok, vá caçar monstros e volte.";
					mes "Eu vou ficar aqui te esperando por um tempo.";
					next;
					break;
				}else{
					mes "[Pesquisador de Chapéis de Abóbora]";
					mes "Espero que isso seja útil. Não se esqueça de usar na batalha com os monstros para pegar as Tortas de Abóbora.";
					delitem 1062,20;	// Jack o' Pumpkin
					getitem 5668,1;		// Weird Pumpkin Hat
					next;
					mes "[Pesquisador de Chapéis de Abóbora]";
					mes "Eu acho que eu deveria voltar para a minha pesquisa.";
					close;
				}
			case 4:
				mes "[Pesquisador de Chapéis de Abóbora]";
				mes "Tchau, até nos vermos outra vez.";
				mes "Eu quero seu bem...";
				close;
		}
	}
}

// Buffs - Trick or Treaters
// =========================
-	script	Doces ou Travessuras::09Treats	706,2,2,{
OnTouch:
	hideoffnpc strnpcinfo(3);
	initnpctimer;
	mes "[Doces ou Travessuras]";
	mes "Hahahaha, FELIZ HALLOWEN!";
	mes "Doces ou Travessuras?";
	next;
	if(select("Travessuras.:Doces.") == 1) {
		mes "[Doces ou Travessuras]";
		mes "!!!!";
		mes "Bom. Eu não tenho escolha, tome a travessura!";
		sc_start SC_Stun,5000,0;
		close;
	}
	mes "[Doces ou Travessuras]";
	mes "Obaa! Que tipo de doce você tem?";
	next;
	switch(select("Bala:Bengala Doce da Sorte:Biscoito de Gengibre:Nenhum")) {
		case 1:
			if(countitem(529) > 0) {
				mes "[Doces ou Travessuras]";
				mes "Obaa, obrigado!";
				mes "Aqui, tome isso por ser tão bom!";
				sc_start SC_STRFood, 1800000, 5;
				sc_start SC_INTFood, 1800000, 5;
				sc_start SC_VITFood, 1800000, 5;
				sc_start SC_AGIFood, 1800000, 5;
				sc_start SC_DEXFood, 1800000, 5;
				sc_start SC_LUKFood, 1800000, 5;
				sc_start SC_FLEEFOOD, 1800000, 15;
				delitem 529,1;	// Candy
				close;
			}else{
				mes "[Doces ou Travessuras]";
				mes "Pelo menos uma Bala, ou um Biscoito de Gengibre, ou até uma Bengala Doce da Sorte, é tudo que eu peço.";
				mes "Bom. Eu não tenho escolha, tome a travessura!";
				sc_start SC_Stun,5000,0;
				close;
			}
		case 2:
			if(countitem(530) > 0) {
				mes "[Doces ou Travessuras]";
				mes "Obaa, obrigado!";
				mes "Aqui, tome isso por ser tão bom!";
				sc_start SC_STRFood, 1800000, 5;
				sc_start SC_INTFood, 1800000, 5;
				sc_start SC_VITFood, 1800000, 5;
				sc_start SC_AGIFood, 1800000, 5;
				sc_start SC_DEXFood, 1800000, 5;
				sc_start SC_LUKFood, 1800000, 5;
				sc_start SC_FLEEFOOD, 1800000, 15;
				delitem 530,1;	// Candy Cane
				close;
			}else{
				mes "[Doces ou Travessuras]";
				mes "Pelo menos uma Bala, ou um Biscoito de Gengibre, ou até uma Bengala Doce da Sorte, é tudo que eu peço.";
				mes "Bom. Eu não tenho escolha, tome a travessura!";
				sc_start SC_Stun,5000,0;
				close;
			}
		case 3:
			if(countitem(538) > 0) {
				mes "[Doces ou Travessuras]";
				mes "Obaa, obrigado!";
				mes "Aqui, tome isso por ser tão bom!";
				sc_start SC_STRFood, 1800000, 5;
				sc_start SC_INTFood, 1800000, 5;
				sc_start SC_VITFood, 1800000, 5;
				sc_start SC_AGIFood, 1800000, 5;
				sc_start SC_DEXFood, 1800000, 5;
				sc_start SC_LUKFood, 1800000, 5;
				sc_start SC_FLEEFOOD, 1800000, 15;
				delitem 538,1;	// Well-baked Cookie
				close;
			}else{
				mes "[Doces ou Travessuras]";
				mes "Pelo menos uma Bala, ou um Biscoito de Gengibre, ou até uma Bengala Doce da Sorte, é tudo que eu peço.";
				mes "Bom. Eu não tenho escolha, tome a travessura!";
				sc_start SC_Stun,5000,0;
				close;
			}
		case 4:
			mes "[Doces ou Travessuras]";
			mes "Pelo menos uma Bala, ou um Biscoito de Gengibre, ou até uma Bengala Doce da Sorte, é tudo que eu peço.";
			mes "Bom. Eu não tenho escolha, tome a travessura!";
			sc_start SC_Stun,5000,0;
			close;
	}
OnInit:
	hideonnpc "Trick or Treater#iRO1";
	hideonnpc "Trick or Treater#iRO2";
	hideonnpc "Trick or Treater#iRO3";
	hideonnpc "Trick or Treater#iRO4";
	hideonnpc "Trick or Treater#iRO5";
	hideonnpc "Trick or Treater#iRO6";
	hideonnpc "Trick or Treater#iRO7";
	hideonnpc "Trick or Treater#iRO8";
	end;

OnEnableTreat:
	enablenpc strnpcinfo(3);
	hideonnpc strnpcinfo(3);
	end;

OnTimer15000:
	hideonnpc strnpcinfo(3);
	disablenpc strnpcinfo(3);
	end;

OnTimer300000:
	donpcevent strnpcinfo(3)+"::OnEnableTreat";
	stopnpctimer;
	end;
}
prontera,151,173,5	duplicate(09Treats)	Trick or Treater#iRO1	706,2,2
prontera,270,350,5	duplicate(09Treats)	Trick or Treater#iRO2	706,2,2
prontera,234,310,5	duplicate(09Treats)	Trick or Treater#iRO3	706,2,2
prontera,156,321,5	duplicate(09Treats)	Trick or Treater#iRO4	706,2,2
prontera,156,283,5	duplicate(09Treats)	Trick or Treater#iRO5	706,2,2
prontera,142,214,5	duplicate(09Treats)	Trick or Treater#iRO6	706,2,2
prontera,134,125,5	duplicate(09Treats)	Trick or Treater#iRO7	706,2,2
prontera,220,72,5	duplicate(09Treats)	Trick or Treater#iRO8	706,2,2

// Monster Summoning - Halloween Wizard
// ====================================
-	script	Bruxo do Halloween#iRO09::09HallowWiz	737,{
	mes "[Bruxo do Halloween]";
	mes "...";
	mes "Você quer pregar uma peça em alguém?";
	next;
	while(1) {
		switch(select("O que, pregar?:Quero:Não.")) {
			case 1:
				mes "[Bruxo do Halloween]";
				mes "Eu posso invocar monstros em outras partes do mundo com apenas alguns materiais.";
				next;
				mes "[Bruxo do Halloween]";
				mes "Interessante, não?";
				next;
				mes "[Bruxo do Halloween]";
				mes "Se você me trouxer Tecido, Zés-Lanternas, Worn Fabric, ou Crushed Pumpkins, eu posso invocar alguns monstros.";
				next;
				break;
			case 2:
				mes "[Bruxo do Halloween]";
				mes "Em qual cidade você quer pregar uma peça?";
				next;
				getmapxy(.@mapname$,.@mapx,.@mapy,0,""+strcharinfo(0)+"");
				if (.@mapname$ == "prontera") {
					switch(select("Geffen:Payon:Alberta:Aldebaran")) {
						case 1:
							set .@HallowTown,3;
							break;
						case 2:
							set .@HallowTown,2;
							break;
						case 3:
							set .@HallowTown,4;
							break;
						case 4:
							set .@HallowTown,5;
							break;
					}
				}
				else if (.@mapname$ == "payon") {
					switch(select("Prontera:Geffen:Alberta:Aldebaran")) {
						case 1:
							set .@HallowTown,1;
							break;
						case 2:
							set .@HallowTown,3;
							break;
						case 3:
							set .@HallowTown,4;
							break;
						case 4:
							set .@HallowTown,5;
							break;
					}
				}
				else if (.@mapname$ == "geffen") {
					switch(select("Prontera:Payon:Alberta:Aldebaran")) {
						case 1:
							set .@HallowTown,1;
							break;
						case 2:
							set .@HallowTown,2;
							break;
						case 3:
							set .@HallowTown,4;
							break;
						case 4:
							set .@HallowTown,5;
							break;
					}
				}
				else if (.@mapname$ == "alberta") {
					switch(select("Prontera:Geffen:Payon:Aldebaran")) {
						case 1:
							set .@HallowTown,1;
							break;
						case 2:
							set .@HallowTown,3;
							break;
						case 3:
							set .@HallowTown,2;
							break;
						case 4:
							set .@HallowTown,5;
							break;
					}
				}
				else if (.@mapname$ == "aldebaran") {
					switch(select("Prontera:Geffen:Payon:Alberta")) {
						case 1:
							set .@HallowTown,1;
							break;
						case 2:
							set .@HallowTown,3;
							break;
						case 3:
							set .@HallowTown,2;
							break;
						case 4:
							set .@HallowTown,4;
							break;
					}
				}
				setarray .@HallowTowns$[1],"prontera","payon","geffen","alberta","aldebaran";
				mes "[Bruxo do Halloween]";
				mes "Ok, então vamos para a próxima etapa.";
				next;
				mes "[Bruxo do Halloween]";
				mes "Quantos Tecidos ou Zés-Lanternas você deseja usar? Não use mais de 100, porque esse número é o máximo que eu posso usar.";
				next;
				input .@input;
				if (.@input == 0) {
					mes "[Bruxo do Halloween]";
					mes "Você ainda não se decidiu.";
					mes "Não é um grande negócio.";
					mes "Volte depois.";
					next;
					break;
				}
				else if (.@input > 100) {
					mes "[Bruxo do Halloween]";
					mes "Eu lhe disse entre 1 a 100!";
					mes "Você não prestou atenção!";
					next;
					break;
				}
				else {
					set .@fabric,countitem(1059);
					set .@jack,countitem(1062);
					set .@worn,countitem(6299);
					set .@crushed,countitem(6298);
					set .@whispers,0;
					set .@darklords,0;

					set .@total,.@fabric + .@jack + .@worn + .@crushed;

					if(.@total < .@input) {
						mes "[Bruxo do Halloween]";
						mes "Conte novamente o número de itens que você tem e me diga o total.";
						mes "Huhuhuhuhuhu...";
						next;
						break;
					}

					if(.@fabric > 0) {
						if(.@fabric >= .@input) {
							delitem 1059,.@input;
							set .@whispers,.@whispers+.@input;
							set .@input,0;
						}
						else{
							delitem 1059,.@fabric;
							set .@input,.@input - .@fabric;
							set .@whispers,.@whispers+.@fabric;
						}
					}
					if(.@worn > 0 && .@input != 0) {
						if(.@worn >= .@input) {
							delitem 6299,.@input;
							set .@whispers,.@whispers+.@input;
							set .@input,0;
						}
						else{
							delitem 6299,.@worn;
							set .@input,.@input - .@worn;
							set .@whispers,.@whispers+.@worn;
						}
					}
					if(.@jack > 0 && .@input != 0) {
						if(.@jack >= .@input) {
							delitem 1062,.@input;
							set .@darklords,.@darklords+.@input;
							set .@input,0;
						}
						else{
							delitem 1062,.@jack;
							set .@input,.@input - .@jack;
							set .@darklords,.@darklords+.@jack;
						}
					}
					if(.@crushed > 0 && .@input != 0) {
						if(.@crushed >= .@input) {
							delitem 6298,.@input;
							set .@darklords,.@darklords+.@input;
							set .@input,0;
						}
						else{
							delitem 6298,.@crushed;
							set .@input,.@input - .@crushed;
							set .@darklords,.@darklords+.@crushed;
						}
					}
					if (.@input > 0) {
						mes "Há um problema.";
						close;
					}
					monster ""+.@HallowTowns$[.@HallowTown]+"",0,0,"Halloween Whisper",3014,.@whispers;
					monster ""+.@HallowTowns$[.@HallowTown]+"",0,0,"Halloween Dark Lord",3015,.@darklords;
					mes "[Bruxo do Halloween]";
					mes "Here's what you wanted.";
					mes "Imagine o que o povo deve estar pensando em outros lugares?";
					mes "Muahahaha";
					close;
				}
			case 3:
				mes "[Bruxo do Halloween]";
				mes "Se você mudar de idéia, volte aqui...";
				mes "Eu vou ficar aqui por um tempo...";
				mes "Kkkk...";
				close;
		}
	}
}
prontera,156,195,5	duplicate(09HallowWiz)	Halloween Wizard#1	737
geffen,133,122,5	duplicate(09HallowWiz)	Halloween Wizard#2	737
payon,156,196,5	duplicate(09HallowWiz)	Halloween Wizard#3	737
alberta,96,57,5	duplicate(09HallowWiz)	Halloween Wizard#4	737
aldebaran,146,122,5	duplicate(09HallowWiz)	Halloween Wizard#5	737