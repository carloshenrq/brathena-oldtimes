//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Kafras da Cidade de Moscóvia           |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.0/1.1                                |
// |--------------------------------------------------------|
// | Descrição: Kafras da Cidade de Moscóvia.               |
// |--------------------------------------------------------|
// | Changelog:                                             |
// | 1.0 Script Traduzido [cleyton]                         |
// | 1.1 Script Revisado [Tyler]                            |
// | Anotações:                                             |
// | - Corrigidas as tabulações no cabeçalho.               |
// \________________________________________________________/

moscovia,223,191,4	script	Assistente Kafra#mosk	114,{
	cutin "kafra_04",2;
	mes "[Assistente Kafra]";
	mes "Bem-vindo à";
	mes "Corporação Kafra.";
	mes "Os serviços da Corporação Kafra";
	mes "sempre estarão ao seu lado.";
	mes "Como posso ajudá-lo?";
	next;
	switch(select("Salvar Ponto de Retorno.:Usar armazém.:Alugar Carrinho.:Checar outras informações.:Cancelar.")) {
		case 1:
			mes "[Assistente Kafra]";
			mes "Seu ponto de retorno";
			mes "foi salvo na cidade de";
			mes "Moscóvia. Obrigado por usar";
			mes "os serviços da Kafra.";
			savepoint "moscovia",221,194;
			close2;
			cutin "", 255;
			end;
		case 2:
			if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
				mes "[Assistente Kafra]";
				mes "Eu lamento, mas você";
				mes "precisa ser um Aprendiz";
				mes "com no mínimo nível básico 6";
				mes "para usar os serviços de armazém.";
				close2;
				cutin "", 255;
				end;
			}
			if(countitem(7059)) delitem 7059,1;
			else {
				if(Zeny<80){
					mes "[Assistente Kafra]";
					mes "Me desculpe, mas você não tem";
					mes "zenys para usar";
					mes "o serviço de armazém.";
					mes "Esse serviço custa 80 zenys.";
					close2;
					cutin "", 255;
					end;
				}
				set Zeny, Zeny-80;
				set RESRVPTS, RESRVPTS + (80/5);
			}
			mes "[Assistente Kafra]";
			mes "Seu armazém";
			mes "está sendo aberto agora.";
			mes "Obrigado por usar";
			mes "o serviço da Kafra.";

			callfunc("F_CheckKafCode");	//check your storage password, if set

			close2;
			openstorage;
			cutin "", 255;
			end;
		case 3:
			if(baseClass != Job_Merchant){
				mes "[Assistente Kafra]";
				mes "Me desculpe, mas";
				mes "o serviço de aluguel de carrinhos de mão";
				mes "está disponível apenas para Mercadores, ";
				mes "Ferreiros, Mestre-Ferreiros,";
				mes "Alquimistas, Criadores e suas evoluções.";
				close2;
				cutin "", 255;
				end;
			}

			else if(checkcart() == 1){
				mes "[Assistente Kafra]";
				mes "Você já possui";
				mes "um carrinho de mão equipado.";
				mes "Infelizmente, nós não podemos";
				mes "alugar mais de um carrinho de";
				mes "mão para um único cliente.";
				close2;
				cutin "", 255;
				end;
			}

			if(countitem(7061) > 0) delitem 7061,1;
			else {
				mes "[Assistente Kafra]";
				mes "O aluguel de um carrinho de mão ";
				mes "custa 700 zenys.";
				mes "Você deseja alugar um carrinho?";
				next;
				if(select("Alugar um carrinho.:Cancelar.") == 1) {
					if(Zeny<700){
						mes "[Assistente Kafra]";
						mes "Eu sinto muito, mas você";
						mes "não possui dinheiro suficiente";
						mes "para pagar o aluguel de um carrinho de mão.";
						mes "O aluguel custa 700 zenys.";
						close2;
						cutin "", 255;
						end;
					}
					set Zeny,Zeny-700;
					set RESRVPTS, RESRVPTS + 48;
				}else{
					close2;
					cutin "", 255;
					end;
				}
			}
			setcart;
			close2;
			cutin "", 255;
			end;
		case 4:
			if(select("Checar Pontos especiais reservados.:Cancelar.") == 2) {
				cutin "", 255;
				close;
			}
			mes "[Assistente Kafra]";
			mes strcharinfo(0) + ", você tem um total de";
			mes RESRVPTS+ " Pontos especiais reservados.";
			next;
			mes "[Assistente Kafra]";
			mes "Você pode trocar suas reservas";
			mes "especiais de Pontos por recompensas no";
			mes "escritório da Corporação Kafra em Aldebaran.";
			mes "Utilize os nossos serviços convenientes para utilizar os benefícios do nosso programa de recompensas.";
			close2;
			cutin "", 255;
			end;
		case 5:
			mes "[Assistente Kafra]";
			mes "Nós, da Corporação Kafra";
			mes "sempre estamos nos esforçando para lhe proporcionar os melhores serviços. Esperamos satisfazer as suas necessidades de aventureiro conforme os padrões de excelência.";
			close2;
			cutin "", 255;
			end;
	}
}