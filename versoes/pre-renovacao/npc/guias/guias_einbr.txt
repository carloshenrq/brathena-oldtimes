//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Guias de Einbroch			 			|
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.5/1.1                                |
// |--------------------------------------------------------|
// | Descrição: Guias da Cidade de Einbroch                 |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [Protimus]                        |
// | 1.1 Script Revisado [Protimus]							|
// \________________________________________________________/

einbroch,72,202,4	script	Guia#ein::EinGuide	852,{
	cutin "ein_soldier",2;
	mes "[Guia de Einbroch]";
	mes "Bem-vindo a Einbroch, a Cidade do Aço.";
	mes "Nós estamos aqui para ajudar os turistas. Então, por favor, sinta-se à vontade para fazer as perguntas que quiser.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Guia da Cidade.:Limpar as indicações do minimapa.:Aviso.:Cancelar.")) {
		case 1:
			mes "[Guia de Einbroch]";
			mes "Basta escolher no menu a seguir:";
			if (.@compass_check == 0) {
				mes "Você gostaria que que eu indicasse os locais em seu Minimapa?";
				next;
				if (select("Sim.:Não.") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				switch(select("^FF0000Aeroporto^000000.:Estação de Trem.:Fábrica.:Praça.:Hotel.:Loja de Armas.:Laboratório.:Guilda dos Ferreiros.:Torre de Einbroch.:Cancelar.")) {
				case 1:
 					mes "[Guia de Einbroch]";
  					mes "O ^FF0000Aeroporto^000000 está localizado a Noroeste. Lá você poderá usar a alegria e o orgulho de nossa cidade, o Aeroplano. Lembre-se de que você precisará pagar uma taxa de admissão para o Aeroplano.";
					if (.@compass_check)
						viewpoint 1,63,228,2,0xFF0000;
					break;
				case 2:
  					mes "[Guia de Einbroch]";
  					mes "A Estação de Trem está a Nordeste. Nós estamos operando trens para Einbech.";
					if (.@compass_check)
						viewpoint 1,236,279,3,0xFF00FF;
					break;
				case 3:
 					mes "[Guia de Einbroch]";
  					mes "A Fábrica é a instalação mais importante em Einbech. Fica na parte sul de nossa cidade.";
					if (.@compass_check)
						viewpoint 1,158,78,4,0xFF00FF;
					break;
				case 4:
  					mes "[Guia de Einbroch]";
  					mes "A Praça está localizada a leste do centro da cidade, aonde fica nossa maior zona comercial.";
					if (.@compass_check)
						viewpoint 1,232,190,5,0xFF00FF;
					break;
				case 5:
  					mes "[Guia de Einbroch]";
  					mes "O Hotel fica a leste da Praça e oferece acomodações de alto nível. Aproveite sua estadia em Einbroch com conforto.";
					if (.@compass_check)
						viewpoint 1,260,201,6,0x00FF00;
					break;
				case 6:
  					mes "[Guia de Einbroch]";
  					mes "A Loja de Armas está localizada ao norte da Praça.";
					if (.@compass_check)
						viewpoint 1,215,221,7,0x00FF00;
					break;
				case 7:
  					mes "[Guia de Einbroch]";
  					mes "O laboratório é um anexo da fábrica e está localizado na esquina sudeste.";
					if (.@compass_check)
						viewpoint 1,36,49,8,0x00FF;
					break;
				case 8:
  					mes "[Guia de Einbroch]";
  					mes "A Guilda dos Ferreiros está localizada a sudeste. Você poderá aperfeiçoar seus itens com os serviços que eles prestam.";
					if (.@compass_check)
						viewpoint 1,244,90,9,0x00FF00;
					break;
				case 9:
  					mes "[Guia de Einbroch]";
  					mes "A Torre de Einbroch está no centro de Einbroch. Você pode visualizar a cidade toda do topo da torre.";
					if (.@compass_check)
						viewpoint 1,174,195,10,0xFFFF00;
					break;
				case 10:
  					mes "[Guia de Einbroch]";
  					mes "Por favor escolha a opção 'Limpar as Indicações do Minimapa' se você não deseja mais ter as indicações dos locais em seu minimapa.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,63,228,2,0xFF0000;
			viewpoint 2,236,279,3,0xFF00FF;
			viewpoint 2,158,78,4,0xFF00FF;
			viewpoint 2,232,190,5,0xFF00FF;
			viewpoint 2,260,201,6,0x00FF00;
			viewpoint 2,215,221,7,0x00FF00;
			viewpoint 2,36,49,8,0x00FF00;
			viewpoint 2,244,90,9,0x00FF00;
			viewpoint 2,174,195,10,0xFFFF00;
			set .@compass_check,0;
			mes "[Guia de Einbroch]";
			mes "As indicações em seu minimapa foram removidas.";
			mes "Volte sempre.";
			break;
		case 3:
			mes "[Guia de Einbroch]";
			mes "Atualizamos nosso sistema de informações de analógico para o digital mais avançado. Esse novo sistema nos permite indicar os locais no minimapa para uma navegação mais simples.";
			mes "Por favor, utilize esse novo sistema de acordo com suas necessidades e vivencie a extraordinária tecnologia de magia e ciência da República de Schwaltzwald.";
			next;
			mes "[Guia de Einbroch]";
			mes "Antes de começar, visualize o mapa no canto superior direito. Se não houver um minimapa em sua tela, aperte '^3355FFCtrl+Tab^000000,'' ou clique no botão ‘mapa’ na janela Informações Básicas.";
			mes "Agora, se você conseguir visualizar o minimapa, clique no ícone '^3355FF-^000000' várias vezes para visualizar a cidade inteira.";
			break;
		case 4:
			mes "[Guia de Einbroch]";
			mes "Esperamos que você aproveite sua viagem.";
			mes "Ah, por favor, tenha em mente que nós costumamos ter uma 'Situação de Emergência de Poluição do Ar'.";
			close2;
			set .@loop1,0;
		}
	}
	cutin "ein_soldier",255;
	end;
}

einbroch,155,43,4	duplicate(EinGuide)	Guia#2ein	852

einbroch,162,317,4	duplicate(EinGuide)	Guia#3ein	852