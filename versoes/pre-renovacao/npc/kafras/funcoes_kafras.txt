//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Funções da Kafra (Functions)           |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 6.3/1.0                                |
// |--------------------------------------------------------|
// | Descrição: These functions handle save, storage,       |
// | cart rental, teleport, and Free Teleport/Warp/Rent Cart|
// | options for all Kafra NPCs.                            |
// |                                                        |
// | Kafra's will need a lot of work. The Welcome Message   |
// | argument may become obsolete, as most kafras have a    |
// | slightly differing Welcome message.                    |
// |                                                        |
// |--------------------------------------------------------|
// | Changelog:                                             |
// | 1.0 Script Traduzido [cleyton]			    |
// | 1.1 Correções conforme Track #27 [cleyton]             |
// | Anotações:                                             |
// | - Corrigidas as tabulações no cabeçalho.               |
// \________________________________________________________/


// Main Function ===========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables

	//Display Kafra Welcome Message
	switch(getarg(0)){
		//Default message (obsolete)
		default:
		case 0:
			mes "[Funcionária Kafra]";
			mes "Bem vindo à";
			mes "corporação Kafra.";
			mes "Os serviços da corporação Kafra";
			mes "sempre estarão ao seu lado.";
			mes "Como posso lhe ajudar?";
			break;

		//Niflheim specific message
		case 1:
			mes "[Funcionária Kafra]";
			mes "^666666B-Be-BeeeemmmV-vindooo";
			mes "para a c-cooorpoor-r-raaçãaaoo";
			mes "K-k-kafraaaa...^000000";
			break;

		//Guild Castle Kafra message
		case 2:
			set @GID,getcharid(2);
			mes "[Funcionária Kafra]";
	 		mes "Bem vindo membro da guilda ^ff0000" + GetGuildName(@GID) + "^000000 .";
			mes "A corporação Kafra, está aqui para atender você.";
			break;

		//Amatsu specific message (obsolete)
		case 3:
			mes "[Funcionária Kafra]";
			mes "Então, você vem de uma terra distante para estudar nossa cultura, ou você só está observando?";
			mes "Em ambos os casos, porque não permanecer por algum tempo?";
			mes "Com a eterna fragância";
			mes "do aroma de flores silvestres.";
			break;

		//Louyang and Ayothaya specific message (obsolete)
		case 4:
			mes "[Funcionária Kafra]";
			mes "Com tantas Kafras";
			mes "espalhadas, você nunca";
			mes "estará longe de casa.";
			break;

		//NPC has it's own welcome message. (Display nothing)
		case 5:
			break;
	}
	next;

	M_Menu:
		cleararray @K_Menu0$[0],"",7;
	switch(getarg(1)){
		// Save and Storage only
		case 1:
			setarray @K_Menu0$[0],"Salvar","Usar armazém","Cancelar";
			break;

		// Storage only
		case 2:
			setarray @K_Menu0$[0],"Usar armazém","Cancelar";
			break;

		// No Teleport (Common)
		case 3:
			setarray @K_Menu0$[0],"Salvar","Usar armazém","Adquirir carrinho de mão","Guia","Checar outra informação","Cancelar";
			break;

		// Case 4 is Einbroch no tele message.

		// No save, or teleport. (Common)
		case 5:
			setarray @K_Menu0$[0],"Usar armazém","Adquirir carrinho de mão","Checar outra informação","Guia","Cancelar";
			break;

		// Storage and Check Other Information only.
		case 6:
			setarray @K_Menu0$[0],"Usar armazém","Guia","Checar outra informação","Cancelar";
			break;

		// Save, Storage, and Pushcart only (Kafra Warehouse)
		case 7:
			setarray @K_Menu0$[0],"Salvar","Usar armazém","Adquirir carrinho de mão","Cancelar";
			break;

		// Save, Storage, Other Check information. (Turbo track)
		case 8:
			setarray @K_Menu0$[0],"Salvar","Usar armazém","Checar outra informação","Cancelar";
			break;

		// Common Kafra Employee
		default:
			setarray @K_Menu0$[0],"Salvar","Usar armazém","Usar teleporte","Adquirir carrinho de mão","Guia","Checar outra informação","Cancelar";
			break;
	}

	// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
	if(getarg(0)==2) {
		cleararray @K_Menu0$[0],"",7;
		setarray @K_Menu0$[0],"Usar armazém","Usar armazém da guilda","Adquirir carrinho de mão","Usar teleporte","Cancelar";
	}
	menu 	@K_Menu0$[0],K_Menu0,@K_Menu0$[1],K_Menu1,@K_Menu0$[2],K_Menu2,
		@K_Menu0$[3],K_Menu3,@K_Menu0$[4],K_Menu4,@K_Menu0$[5],K_Menu5,
		@K_Menu0$[6],K_Menu6,@K_Menu0$[7],K_Menu7;
	K_Menu0:
		set @num,0;
		goto K_Menuf;
	K_Menu1:
		set @num,1;
		goto K_Menuf;
	K_Menu2:
		set @num,2;
		goto K_Menuf;
	K_Menu3:
		set @num,3;
		goto K_Menuf;
	K_Menu4:
		set @num,4;
		goto K_Menuf;
	K_Menu5:
		set @num,5;
		goto K_Menuf;
	K_Menu6:
		set @num,6;
		goto K_Menuf;
	K_Menu7:
		set @num,7;
	K_Menuf:
		if (@K_Menu0$[@num] == "Salvar") return;
		if (@K_Menu0$[@num] == "Usar armazém"){
			// Do not charge for Guild Storage
			if(getarg(0) == 2) callfunc "F_KafStor",2,0,0;
			else callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Usar teleporte"){
			// Display Einbroch "No Teleport Service" notice.
			if(getarg(1) == 4){
				mes "[Funcionária Kafra]";
				mes "Por causa do ^FF0000Acordo de limite";
				mes "de transporte^000000, a";
				mes "corporação Kafra não pode";
				mes "fornecer serviços de teletransporte";
				mes "aqui na república de Schwaltzvalt.";
				next;
				mes "[Funcionária Kafra]";
				mes "Nós solicitamos a você utilizar";
				mes "o serviço de Aeroplano no lugar";
				mes "do teletransporte. Muito obrigado por sua";
				mes "compreensão e cooperação.";
				next;
				goto M_Menu;
			}
			callfunc "F_KafTele",getarg(0);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Adquirir carrinho de mão"){
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Guia"){
			callfunc "F_KafGuide";
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Checar outra informação"){
			callfunc "F_KafInfo",getarg(2);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "Cancelar"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		if (@K_Menu0$[@num] == "Usar armazém da guilda"){
			callfunc "F_KafStor",1,0;
			next;
			goto M_Menu;
		}

}


// Storage Function =======================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage() == 1){
			mes "[Funcionária Kafra]";
			mes "Sinto muito, mas outro membro do Clã está usando o armazém neste instante.";
			mes "Por favor, aguarde até que esta pessoa termine.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}

	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
		mes "[Funcionária Kafra]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-D-ddeessssculllpppe,";
			mes "v-vo-voooccêe éee umm";
			mes "Appreenddizz... P-Prreeecisa teerrr";
			mes "nívveelll d-de hh-habibilidaddeee level 6...^000000";
			return;
		}
		mes "Lamento, mas você";
		mes "precisa ser um Aprendiz com ";
		mes "nível mínimo de habilidade";
		mes "level 6 para usar o armazém.";
		return;
	}

	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Funcionária Kafra]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "P-Preeciso de z-zeny...!";
					mes "P-Preciiiso de 150... zenys...";
					mes "Ergh!!! T-tomando e-energia!";
					return;
				}
				//Standard Message
				mes "Lamento, mas você não possui";
				mes "zenys suficientes para usar";
				mes "o serviço de armazém.";
				mes "Para acessar o armazém, custa"+getarg(1)+" zenys.";
				return;
			}
			set Zeny, Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Funcionária Kafra]";
			mes "^666666Muito obrigado.. por... usar...";
			mes "Muito obrigado... por... usar...";
			mes "Muito obrigado... por... usar...";
			mes "Muito obrigado... por... usar...";
			mes "Muito obrigado... por... usar...^000000";
		}
		// Normal message
		else {
			mes "[Funcionária Kafra]";
			mes "Nesse momento, estou";
			mes "abrindo seu armazém.";
			mes "Muito obrigado por usar";
			mes "os serviços da corporação Kafra.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set

	close2;
	openstorage;
	cutin "", 255;
	end;
}


// Teleport Function ==================================================
function	script	F_KafTele	{
	mes "[Funcionária Kafra]";
	mes "Por favor, escolha";
	mes "seu destino.";
	next;

	menu 	@wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3,
		@wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Cancel") return;
		// Consume "Free Ticket for Kafra Transportation" if available.
		// Do not consume if in Guild castle
		if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
		else {
			if (Zeny<@wrpP[@num]){
				mes "[Funcionária Kafra]";
				mes "Sinto muito, mas você não possui";
				mes "zenys para teleportar.";
				mes "O serviço de teleporte ";
				mes "para"+@wrpD$[@num]+"custa"+@wrpP[@num]+" zenys.";
				close2;
				cutin "", 255;
				end;
			}
			set Zeny, Zeny-@wrpP[@num];
			set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);
		}
		if (@wrpD$[@num] == "Alberta") warp "alberta", 117, 56;
		if (@wrpD$[@num] == "Al De Baran") warp "aldebaran",168,112;
		if (@wrpD$[@num] == "Comodo") warp "comodo", 209, 143;
		if (@wrpD$[@num] == "Izlude") warp "izlude", 91, 105;
		if (@wrpD$[@num] == "Geffen") warp "geffen", 120, 39;
		if (@wrpD$[@num] == "Morroc") warp "morocc", 156, 46;
		if (@wrpD$[@num] == "Payon") warp "payon", 161, 58;
		if (@wrpD$[@num] == "Prontera") warp "prontera", 116, 72;
		if (@wrpD$[@num] == "Mina Abandonada de Mjolnir") warp "mjolnir_02", 99, 351;
		if (@wrpD$[@num] == "Ilha do Farol, Pharos") warp "cmd_fild07", 127, 134;
		if (@wrpD$[@num] == "Calabouço dos Orcs") warp "gef_fild10", 52, 326;
		if (@wrpD$[@num] == "Umbala") warp "umbala", 100, 154;
		if (@wrpD$[@num] == "Juno") warp "yuno", 158, 125;
		cutin "", 255;
		end;
}


// Cart Function ========================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(baseClass != Job_Merchant){
		mes "[Funcionária Kafra]";
		mes "Eu lamento, mas";
		mes "o serviço de aluguel de carrinho de mão";
		mes "está disponível somente para Mercadores,";
		mes "Ferreiros, Mestre Ferreiros,";
		mes "Alquimistas e Criadores.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Funcionária Kafra]";
		mes "Você já possui";
		mes "um carrinho de mão equipado.";
		mes "Automáticamente, você não poderá";
		mes "alugar outro carrinho de mão";
		mes "ao mesmo tempo.";
		return 1;
	}

	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Funcionária Kafra]";
		mes "O aluguel do carrinho de mão";
		mes "custará "+getarg(1)+" zenys.";
		mes "Você deseja alugar um carrinho de mão?";
		next;
		menu "Alugar um carrinho de mão",-, "Cancelar",M_End;
			if(Zeny<getarg(1)){
				mes "[Funcionária Kafra]";
				mes "Lamento, mas você";
				mes "não possui ";
				mes "zenys suficientes para alugar um carrinho de mão.";
				mes "O aluguel custa "+getarg(1)+" zenys.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;

M_End:
	return 0;
}


// Pass Function ===============================================================
function	script	F_KafGuide	{
	mes "[Funcionária Kafra]";
	mes "Acesse os guias da cidade.";
	return;
}

// Special Reserve Points Function ===========================================
function	script	F_KafInfo	{

	sM_Menu:
//Uncomment next line to block Kafra Storage Protection
//	if(getarg(0) == 0) menu "Checar reserva de pontos especiais.",sM_ResChk, "Localização das funcionárias Kafra",sM_KafLoc, "Cancelar",sM_End;
	if(getarg(0) == 0) menu "Checar reserva de pontos especiais.",sM_ResChk, "Serviço de senha para armazém",sM_KafCode, "Localização das funcionárias Kafra",sM_KafLoc, "Cancelar",sM_End;

	sM_ResChk:
		mes "[Funcionária Kafra]";
		mes "Vamos ver...";
		mes strcharinfo(0) + "...";
		mes "Ah, você tem um total de ";
		mes RESRVPTS+ " reservas de pontos especiais.";
		next;
		mes "[Funcionária Kafra]";
		mes "Você pode trocar suas";
		mes "Reservas de pontos especiais por";
		mes "recompensas no escritório da corporação Kafra em Aldebaran. Por favor,";
		mes "utilize nossos serviços convenientes receber os benefícios do nosso programa de recompensas.";
		next;
		if(getarg(0) == 1) return;
		goto sM_Menu;

	sM_KafLoc:
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		next;
		viewpoint 2,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 2,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 2,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 2,@viewpX[3],@viewpY[3],4,0xFF00FF;
		goto sM_Menu;

	sM_KafCode:
		callfunc("F_SetKafCode","[Funcionária Kafra]","Kafra Services");

	sM_End:
		return;
}


// End Function =====================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Funcionária Kafra]";
	//Save
	if(getarg(1)==1) {
		mes "Seu ponto de renascimento";
		mes "foi salvo aqui.";
		mes getarg(2)+".";
		mes "Muito obrigado por usar";
		mes "os serviços da corporação Kafra.";
	}

	// Generic End
	else if(getarg(0)==0 || getarg(0)==5) {
		mes "Nós da corporação Kafra,";
		mes "sempre estamos nos esforçando para proporcionoar os melhores serviços. Esperamos satisfazer as suas necessidades de aventureiro nos seus padrões de excelência.";
	}

	// Niflheim End
	else if(getarg(0)==1) {
		percentheal 0,- 25;
		mes "^666666Kaffffra n-nunca";
		mes "mooooorrrrre. Semprre...";
		mes "Es-ta  ao seeeuuu laaadooo~^000000";
	}

	close2;
	cutin "", 255;
	end;
}

// Check Storage Password Function ====================
function	script	F_CheckKafCode	{
	if(#kafra_code==0) return;
	mes "Entra com a senha do armazém:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "Senha errada.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// Set / Change / Clear Storage Password Function ====================
// getarg(0) = NPC Name, getarg(1) = Company Name
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "Seu armazém está protegido com uma senha. O que você fará agora?";
		next;
		menu "Mudar senha antiga -> 5000z",-,
		"Remover a senha do armazém -> 1000z",M_CLEAR,
		"Cancelar",M_END;
	} else {
		mes ""+getarg(1)+" orgulhosamente apresenta um novo serviço:";
		mes "Proteção adicional do seu armazém com senha.";
		next;
		menu "Definir nova senha -> 5000z",M_SET,
		"Cancelar",M_END;
	}

	mes getarg(0);
	mes "Primeiro, digite sua ^0000FFsenha antiga^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0 || @code != #kafra_code) {
		mes "Senha errada. Você não pode inserir uma nova senha.";
		emotion e_hmm;
		goto M_END;
	}
	next;

M_SET:
	mes getarg(0);
	mes "Agora entre com sua ^FF0000nova senha^000000 para proteger seu armazém de roubos.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "A senha não foi trocada.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) goto L_ZENY;
	set Zeny,Zeny-5000;
	//set RESRVPTS, RESRVPTS + (5000/50);	//hardcoded password doesn't add pts

	set #kafra_code,@code;
	mes "Seu armazém está protegido com uma senha secreta.";
	mes "Obrigado por usar"+getarg(1)+".";
	emotion e_thx;
	goto M_END;

M_CLEAR:
	mes getarg(0);
	mes "Por favor, entre com a senha antes de sua retirada.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "A senha não foi removida.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes getarg(0);
	if(Zeny < 1000) goto L_ZENY;
	set Zeny,Zeny-1000;
	//set RESRVPTS, RESRVPTS + (1000/50);	//hardcoded password doesn't add pts
	if(@code == #kafra_code) {
		set #kafra_code,0;
		mes "Você teve êxito ao colocar senha em seu armazém.";
		mes "Muito obrigado por usar os "+getarg(1)+".";
		emotion e_thx;
	} else {
		mes "Senha errada! Não devolverei seus 1000 zenys.";
		mes "Por favor, na próxima entre com a senha correta.";
		emotion e_sry;
	}
	goto M_END;

L_ZENY:
	mes "Você não possui zenys.";
	emotion e_cash;
M_END:
	close2;
	cutin "",255;
	end;
}

// Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "Entre com um número 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: Tentou colocar senha de armazenamento.";
	}
	if(input(@code_) == 1) {
		mes "Você não pode usar uma senha tão grande.";
		return 0;
	}
	if(@code_<1000) {
		mes "Você não pode usar uma senha tão curta.";
		return 0;
	}
	return @code_;
}

