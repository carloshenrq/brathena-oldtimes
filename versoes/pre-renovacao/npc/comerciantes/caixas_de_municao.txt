//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Negociador de Carregadores Kenny		|
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.3/1.1                                |
// |--------------------------------------------------------|
// | Descrição:	Troca Projéteis por Carregadores/Pacotes.	|
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [Mark]	                        |
// | 1.1 Script Otimizado [Mark]	                        |
// | - Anotações		   			    					|
// \________________________________________________________/

que_ng,187,149,3	script	Negociador de Carregadores Kenny	83,{
	if(BaseJob == Job_Gunslinger) {
		mes "[Kenny]";
		mes "Bem-vindo à minha loja de carregadores.";
		mes "Como você sabe, um número maior de projéteis podem ser carregados mais facilmente quando estão em carregadores.";
		mes "Agora, posso ajudá-lo em algo em particular?";
		next;
		switch(select("Pacote de Esferas de Relâmpago:Pacote de Esferas Cegantes:Pacote de Esferas Venenosas:Pacote de Esferas Congelantes:Pacote de Esferas de Chamas:Carregador:Carregador Sanguinário:Carregador de Prata:Cancelar")) {
			case 1: callfunc "Func_Casing",13204,12144; break;
			case 2: callfunc "Func_Casing",13206,12145; break;
			case 3: callfunc "Func_Casing",13205,12146; break;
			case 4: callfunc "Func_Casing",13207,12147; break;
			case 5: callfunc "Func_Casing",13203,12148; break;
			case 6: callfunc "Func_Casing",13200,12149; break;
			case 7: callfunc "Func_Casing",13202,12150; break;
			case 8: callfunc "Func_Casing",13201,12151; break;
			case 9:
				mes "[Kenny]";
				mes "Bem, se você achar que tem muitos projéteis, venha me encontrar.";
				mes "É uma boa idéia armazenar projéteis em Carregadores.";
				close;
		}
	}
	mes "[Kenny]";
	mes "Bem-vindo à minha loja.";
	mes "Aqui, eu forneço Pacotes e Carregadores para Justiceiros.";
	mes "Desculpe, mas não parece que meus serviços serão úteis para você, aventureiro.";
	next;
	mes "[Kenny]";
	mes "Heh, mas se você conhecer algum Justiceiro, mande-o para mim.";
	mes "Ninguém pode carregar tantos projéteis.";
	close;
}

function	script	Func_Casing	{
	mes "[Kenny]";
	mes "Agora, você pode trocar";
	mes "500 "+getitemname(getarg(0));
	mes "e 500 zeny por 1 "+getitemname(getarg(1))+", tenha certeza";
	mes "que você tem projéteis o suficiente e zeny para esta troca.";
	next;
	mes "[Kenny]";
	mes "Você pode trocar 500 "+getitemname(getarg(0))+" e 500 zeny por 1 "+getitemname(getarg(1));
	next;
	mes "[Kenny]";
	mes "Lembre-se que não posso lhe dar mais que 50 Carregadores por vez.";
	mes "Agora digite o número de Carregadores que você quer receber.";
	mes "Se você deseja cancelar, digite '0'.";
	next;
	input .@amount;
	if(.@amount > 50 || .@amount == 0) {
		mes "[Kenny]";
		mes "Ei, não posso lhe dar esse número de Carregadores.";
		mes "Por favor tente novamente, e digite um número menor que 50.";
		close;
	}
	if (countitem(getarg(0)) >= (500*.@amount)) {
		if (Zeny >= (500*.@amount)) {
			if (checkweight(getarg(1),.@amount) == 0) {
				mes "[Kenny]";
				mes "Ei, você tem muito lixo no seu inventário.";
				mes "Libere algum espaço e volte para trocar seus projéteis por alguns Carregadores depois, ok?";
				close;
			}else{
				mes "[Kenny]";
				mes "Certo, aqui estão seus Carregadores.";
				mes "Obrigado por visitar minha loja, e espero que use sua munição com sabedoria.";
				set Zeny,Zeny-500*.@amount;
				delitem getarg(0),500*.@amount;
				getitem getarg(1),.@amount;
				close;
			}
		}else{
			mes "[Kenny]";
			mes "Desculpe, mas você não possui zeny o suficiente para esta troca de Carregadores.";
			mes "Volte à minha loja quando você tiver algum dinheiro.";
			close;
		}
	}else{
		mes "[Kenny]";
		mes "Desculpe, mas você não possui projéteis o suficiente para esta troca de Carregadores.";
		mes "Talvez você deveria checar novamente seu inventário, e voltar mais tarde.";
		close;
	}
}