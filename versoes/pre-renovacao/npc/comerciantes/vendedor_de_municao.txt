//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Negociador de Projéteis Tony			|
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.4/1.0                                |
// |--------------------------------------------------------|
// | Descrição:	Negociador de Projéteis.					|
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [Mark]	                        |
// | - Anotações		   			    					|
// \________________________________________________________/

que_ng,187,156,3	script	Negociador de Projéteis Tony	86,{
	if (BaseJob == Job_Gunslinger) {
		mes "[Tony]";
		mes "Eu sou Tony, o negociador de projéteis.";
		mes "Venha a mim sempre que precisar de munições.";
		mes "Apenas me traga os materiais, e eu irei lhe fazer os projéteis que precisar.";
		next;
		switch(select("Esfera Venenosa:Esfera de Chamas:Esfera de Relâmpagos:Esfera Cegante:Esfera Congelante:Cancelar")) {
		case 1: callfunc "Bullet_Trade",937,10,13205; break;
		case 2: callfunc "Bullet_Trade",7097,2,13203; break;
		case 3: callfunc "Bullet_Trade",7053,3,13204; break;
		case 4: callfunc "Bullet_Trade",1024,5,13206; break;
		case 5: callfunc "Bullet_Trade",7054,2,13207; break;
		case 6:
			mes "[Tony]";
			mes "Mudou de idéia?";
			mes "Ok, volte sempre que precisar de projéteis, estarei sempre aqui.";
			close;
		}
	}
	mes "[Tony]";
	mes "Ei, sou Tony. Eu sou o responsável pela distribuição e fabricação de projéteis para Justiceiros.";
	next;
	mes "[Tony]";
	mes "Lamento se você veio para comprar munição.";
	mes "Eu só posso negociar com Justiceiros credenciados.";
	close;
}

function	script	Bullet_Trade	{
	mes "[Tony]";
	mes "Para cada 30 "+getitemname(getarg(2))+",";
	mes "você deve me entregar 1 Emveretarcon, 1 Fracon e "+getarg(1)+" "+getitemname(getarg(0))+".";
	next;
	mes "[Tony]";
	mes "Lembre que só posso entregar um máximo de 500 caixas de 30 projéteis por negociação.";
	mes "Por favor entre com o número de caixas de projéteis que você gostaria.";
	mes "Caso queira parar a negociação, apenas digite '0'.";
	next;
	input .@amount;
	if (.@amount < 1 || .@amount > 500) {
		mes "[Tony]";
		mes "Ei, não posso lhe dar tantos projéteis.";
		mes "Não esqueça de digitar um número menor que 500 se você quiser trocar alguns itens por alguns projéteis.";
		close;
	}
	if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(getarg(0)) >= (.@amount*getarg(1))) {
		if (checkweight(getarg(2),.@amount * 30) == 0) {
			mes "[Tony]";
			mes "Nossa! Seu inventário não possui espaço o suficiente para esta quantia de projéteis.";
			mes "Volte quando tiver mais espaço no inventário.";
			mes "Tente guardar algumas coisas no Armazém da Kafra.";
			close;
		}else{
			mes "[Tony]";
			mes "Muito bom, tudo parece estar em ordem.";
			mes "Deixe-me pegar esses materiais, e aqui estão seus projéteis.";
			mes "Foi um prazer negociar com você~";
			delitem 1010,.@amount;
			delitem 1011,.@amount;
			delitem getarg(0),.@amount * getarg(1);
			getitem getarg(2),.@amount * 30;
			close;
		}
	}else{
		mes "[Tony]";
		mes "Humm. Parace que você não possui os materiais que necessito.";
		mes "Bem, isso não é problema.";
		mes "Apenas volte aqui quando pegar tudo que preciso, ok?";
		close;
	}
}
