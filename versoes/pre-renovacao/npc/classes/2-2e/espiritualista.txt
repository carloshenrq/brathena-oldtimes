//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: Quest do Espiritualista                |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.5/1.2                                |
// |--------------------------------------------------------|
// | Descrição: Mudança de Classe para Espiritualista       |
// | [Vars Used]											|
// | SOUL_Q = Job Change Phase. (Max 4)						|
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Otimização [Wolf]                                  |
// | 1.1 Otimizações no npc [Wolf]                          |
// | 1.2 Script Traduzido [Wolf]				    		|
// \________________________________________________________/

morocc_in,174,30,6	script	Kid#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Boa sorte em suas";
		mes "jornadas. À medida que você";
		mes "dominar mais suas habilidades,";
		mes "você será capaz de drenar";
		mes "mais poderes espirituais";
		mes "à seus aliados...";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[Criança]";
		mes "Você não é um guerreiro "+((Sex)?"do sol?":"da lua?")+", conheço";
		mes "os seus caminhos. Afinal,";
		mes "a base de nossas habilidades";
		mes "é fundamentada na";
		mes "classe Taekwon, certo?";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[Criança]";
		mes "Hm? Eu não tenho nada";
		mes "para te ofecer. Mas se você";
		mes "souber de alguns praticantes";
		mes "experientes de Taekwon Do,";
		mes "eles poderiam se beneficiar";
		mes "com os meus conhecimentos.";
		close;
	}
	if (JobLevel < 40) {
		mes "[Criança]";
		mes "Então você está estudando";
		mes "Taekwon Do. Isso é bom,";
		mes "isso é muito bom. Apenas";
		mes "mantenha-se treinando";
		mes "e aprimorando as suas habilidades.";
		close;
	} else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[Criança]";
			mes "...";
			mes "Ei você.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Queria falar comigo?";
			next;
			mes "[Criança]";
			mes "Sim, queria.";
			mes "Não me faça";
			mes "levantar a minha voz, e";
			mes "é saia agora daqui.";
			next;
			if (select("Você é muito irritado para uma criança!:Ignorá-lo.") == 1) {
				mes "[Criança]";
				mes "Sua sorte é que";
				mes "eu estou me interessando";
				mes "por você! Eu posso parecer";
				mes "uma criança, mas eu tenho";
				mes "mais de 300 anos de idade!";
				emotion e_pif;
				next;
				mes "[Criança]";
				mes "Agora ouça...";
				mes "Sei que você é um";
				mes "discípulo do Taekwondo.";
				mes "É uma arte respeitável, porém";
				mes "eu tneho uma proposta para";
				mes "você, se quiser.";
				emotion e_heh;
				next;
				mes "[Criança]";
				mes "Olhando para você, eu vejo";
				mes "que você tem uma prosperidade";
				mes "espiritual muito grande. Você tem";
				mes "um grande potencial e não quero";
				mes "ver esse potencial desperdiçado. Por que";
				mes "você não se torna um 'Espiritualista'?'";
				next;
				if (select("Ha! Menino bobo ~:Espiritualista?") == 1) {
					mes "[Criança]";
					mes "Você... Você n-não";
					mes "acredita em mim? Estou";
					mes "falando sério. Você pode";
					mes "esquecer o fato de que eu";
					mes "sou uma criança por apenas um";
					mes "minuto? *Psh* ...Jovens.";
					close;
				}
				mes "[Criança]";
				mes "Os espiritualistas conversam";
				mes "com espíritos guerreiros,";
				mes "que ainda gostariam de lutar";
				mes "no mundo dos vivos. Agora, esses";
				mes "espíritos guerreiros não podem mais";
				mes "lutar com o corpo deles nesse mundo.";
				next;
				mes "[Criança]";
				mes "No entanto, desde que seu";
				mes "espírito esteja bem preparado,";
				mes "esses espíritos são atraidos para você.";
				mes "Com muito treinamento, você poderá";
				mes "temporariamente invocar esses espíritos para seus aliados.";
				next;
				mes "[Criança]";
				mes "Mas, você não pode usar o";
				mes "poder desses espíritos. Além disso,";
				mes "dependendo de suas habilidades";
				mes "como Espiritualista, você poderá";
				mes "encantar outros personagens de outras classes com esses espíritos.";
				next;
				mes "[Criança]";
				mes "Você terá que entrar";
				mes "em um mundo completamente";
				mes "diferente para se tornar um Espiritualista,";
				mes "mas eu sei que você irá conseguir.";
				mes "Então, o que você me diz?";
				next;
				if (select("Não. Pelo menos, não agora...:Tudo bem, o que eu devo fazer?") == 1) {
					mes "[Criança]";
					mes "Ah, então tudo bem. Bem,";
					mes "se você decidir se tornar";
					mes "um Espiritualista,";
					mes "volte aqui e fale comigo";
					mes "a qualquer momento.";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[Criança]";
				mes "Então você quer se tornar";
				mes "um Espiritualista? Ótimo!";
				mes "Tudo bem, primeiramente você";
				mes "terá que me trazer alguns itens.";
				mes "Não se preocupe, mais tarde eu";
				mes "te explico a importância deles.";
				next;
				mes "[Criança]";
				mes "Então, tragá-me";
				mes "^0000FF1 Diamante de 3 Quilates ^000000,";
				mes "^0000FF1 Corações Imortal^000000 e";
				mes "^0000FF1 Rosa Eterna^000000.";
				mes "E não me faça esperar";
				mes "muito tempo, tudo bem?";
				close;
			}
			mes "[Criança]";
			mes "Ei...!?";
			mes "Espere, aonde você";
			mes "está indo? Eu...";
			mes "Eu quero falar com você!";
			close;
		} else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[Criança]";
				mes "Você está de volta, hein?";
				mes "Então você trouxe";
				mes "^0000FF1 Diamante de 3 Quilates ^000000,";
				mes "^0000FF1 Corações Imortal^000000 e";
				mes "^0000FF1 Rosa Eterna^000000.";
				mes "like I asked you to?";
				next;
				if (select("Trouxe, aí está.:Não, não trouxe...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; //Crystal_Jewel__
						delitem 929,1; //Immortal_Heart
						delitem 748,1; //Witherless_Rose
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[Criança]";
						mes "Ótimo, vejo que você";
						mes "trouxe tudo. Mas antes";
						mes "de começarmos, deixe-me";
						mes "apresentar. Meu nome é";
						mes "Maia, e eu estou vivo a 300 anos.";
						next;
						mes "[Maia]";
						mes "Sem revelar muitos";
						mes "detalhes, eu fui divinamente escolhido para";
						mes "recrutar mais Espiritualistas. Isso é parte da razão";
						mes "pela qual, você sabe, eu não faleci.";
						next;
						mes "[Maia]";
						mes "De qualquer jeito, eu tenho que";
						mes "terminar os preparativos com os";
						mes "itens que você trouxe, teria como";
						mes "você voltar aqui daqui a pouco?";
						mes "Então, daqui a pouco nos falamos.";
						close;
					}
					mes "[Criança]";
					mes "Hm...?";
					mes "Você esqueceu";
					mes "de algumas coisas. Volte";
					mes "e tragá-me todos os itens";
					mes "corretamente, não demore,";
					mes "ok?";
					emotion e_pif;
					next;
					mes "[Criança]";
					mes "Eu sei que acabei de dizer";
					mes "o que nós precisamos, mas vou";
					mes "dizer novamente:";
					mes "^0000FF1 Diamante de 3 Quilates ^000000,";
					mes "^0000FF1 Corações Imortal^000000 e";
					mes "^0000FF1 Rosa Eterna^000000.";
					close;
				}
				mes "[Criança]";
				mes "Hm. Tudo bem.";
				mes "Embora eu tenha";
				mes "todo tempo do mundo";
				mes "Eu não gosto de";
				mes "esperar por muito tempo.";
				close;
			}
			set SOUL_Q,0;
			mes "[Criança]";
			mes "Você se tornará um guerreiro";
			mes "do Sol, da Lua, e";
			mes "das Estrelas ao invés disso? Eu não tinha idéia";
			mes "que você tinha um potencial assim.";
			mes "Acho que eu não posso culpá-lo...";
			close;
		} else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[Maia]";
				mes "Você ainda tem";
				mes "Pontos de Habilidades.";
				mes "Use todos os pontos para";
				mes "aprender as habilidades do Taekwondo, e volte";
				mes "quando você estiver pronto.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Agora, alguém está completando";
				mes "uma cerimônia para se";
				mes "tornar um Espiritualista.";
				mes "Você por favor pode aguardar até";
				mes "que acabe? Então, quando eu estiver";
				mes "desocupado, eu te atenderei.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Ótimo, eu terminei os";
			mes "preparativos. Agora";
			mes "vamos prosseguir com";
			mes "a cerimônia de se tornar";
			mes "um Espiritualista.";
			mes "Comece feche seus olhos...";
			close2;
			warp "job_soul",30,30;
			end;
		} else if (SOUL_Q > 2) {
			mes "[Maia]";
			mes "Você está pronto para";
			mes "entrar nas profundezas";
			mes "de sua mente de novo?";
			next;
			if (select("Não:Sim") == 1) {
				mes "[Maia]";
				mes "Então tudo bem, volte";
				mes "aqui quando você";
				mes "achar que está pronto.";
				mes "Até mais, estarei te";
				mes "esperando por aqui.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Agora, alguém está completando";
				mes "uma cerimônia para se";
				mes "tornar um Espiritualista.";
				mes "Você por favor pode aguardar até";
				mes "que acabe? Então, quando eu estiver";
				mes "desocupado, eu te atenderei.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Tudo bem, agora";
			mes "feche seus olhos e relaxe.";
			mes "Vamos voltar para as";
			mes "profundezas de sua mente.";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	Maia#link2::SLTester	-1,3,3,{
OnTouch:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			mes "[Maia]";
			mes "Hm? Como você chegou";
			mes "aqui? Você ainda não foi qualificado";
			mes "para essa cerimônia. Venha, vou te enviar de volta para Morroc...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[Maia]";
			mes "Você reconhece esse";
			mes "lugar? Agora, estamos";
			mes "dentro de sua mente. Os espíritos";
			mes "dos guerreiros que morreram,";
			mes "estão aqui, esperando você";
			mes "invocar os seus poderes.";
			next;
			mes "[Maia]";
			mes "Neste momento, só alguns deles";
			mes "estão aqui, mas se você";
			mes "continuar treinando, você";
			mes "será capaz de invocar mais";
			mes "espíritos como um Espiritualista.";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[Maia]";
			mes "Nós só podemos permanecer";
			mes "dentro de sua mente por 3 minutos.";
			mes "Sugiro que você fale com";
			mes "os espíritos, quando você";
			mes "estiver com oportunidade.";
			close;
		} else if (SOUL_Q == 3) {
			mes "[Maia]";
			mes "Ouça o que os espíritos";
			mes "tendem a dizer.";
			mes "Há uma razão pela";
			mes "qual eles não podem";
			mes "ir para o próximo mundo.";
			close;
		} else if (SOUL_Q == 4) {
			mes "[Maia]";
			mes "Acredito que você";
			mes "esteja pronto para se tornar";
			mes "um Espiritualista. No entanto,";
			mes "você pode continuar";
			mes "falando com os espíritos,";
			mes "se for esse o seu desejo.";
			next;
			if (select("Conversar mais com os espíritos.:Me tornar um Espiritualista.") == 1) {
				mes "[Maia]";
				mes "Alright. Try to hurry";
				mes "since we can remain in";
				mes "your mind for a limited";
				mes "time. Although, we can";
				mes "go back inside your mind";
				mes "if you talk to me later...";
				close;
			}
			mes "[Maia]";
			mes "Então vamos começar com a";
			mes "cerimônia. Esses itens serão";
			mes "usados para te dotar com a";
			mes "capacidade de pegar o poder";
			mes "dos guerreiros falecidos e";
			mes "fortalecer seus aliados nas batalhas.";
			next;
			mes "[Maia]";
			mes "Essa Rosa Eterna vai";
			mes "murchar ao invés de você...";
			specialeffect EF_MAPPILLAR2,AREA,"Maia#link2";
			next;
			mes "[Maia]";
			mes "Essa Rosa Eterna vai";
			mes "enfraquecer ao invés de você...";
			mes "O Coração Imortal irá bombear";
			mes "o sangue, ao invés do seu.";
			next;
			mes "[Maia]";
			mes "Essa Rosa Eterna vai";
			mes "enfraquecer ao invés de você...";
			mes "O Coração Imortal irá bombear";
			mes "o sangue, ao invés do seu. Esse diamente virará pó,";
			mes "no lugar do seu corpo mortal.";
			next;
			mes "[Maia]";
			mes "Os mortos que desejam";
			mes "continuar a luta...";
			mes "Vão lutar por você! Use seus";
			mes "poderes como um Espiritualista";
			mes "sabiamente e para coisas certas.";
			next;
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";		// clears all job variables for the current player
			set SOUL_Q,0;
			mes "[Maia]";
			mes "Te desejo a maior sorte";
			mes "em sua nova vida. Cerque-se";
			mes "de amigos e aliados, e os";
			mes "espíritos serão capazes de";
			mes "te proteger e te ajudar em suas batalhas. Adeus, amigo.";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
		mes "[Maia]";
		mes "Hmm...?";
		mes "Ainda não está";
		mes "na sua hora de";
		mes "estar aqui. Irei te enviar";
		mes "de volta para Morroc...";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Chegou a hora de você";
		mes "se aventurar em todo o";
		mes "mundo! Mais Espiritualistas";
		mes "com certeza serão necessários para a batalha contra o mal...";
	} else {
		mes "[Maia]";
		mes "Que estranho...";
		mes "Você não deveria";
		mes "estar aqui. Te enviarei";
		mes "de volta para Morroc...";
	}
	close2;
	warp "morocc",157,47;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	Maia#link6	716

job_soul,30,35,6	script	Monk Spirit#link4	827,{
	if (SOUL_Q == 2) {
		mes "[Espírito de Monge]";
		mes "Quem sou eu..?";
		mes "Eu acho... Eu acho";
		mes "melhor que você";
		mes "converse com o Maia primeiro...";
		mes "Quem sou eu, é realmente uma";
		mes "explicação muito complicada...";
		close;
	} else if (SOUL_Q > 2) {
		mes "[Espírito de Monge]";
		mes "Na vida, meus amigos";
		mes "sempre me falavam que";
		mes "eu realizaria tudo que esparava";
		mes "como um Monge. Apesar que...";
		mes "Apesar que eu nunca estivesse";
		mes "planamente satisfeito com minhas habilidades.";
		next;
		mes "[Espírito de Monge]";
		mes "Na morte, eu me arrependi por não";
		mes "ter repassado meus conhecimentos";
		mes "para futuros Monges.";
		mes "Concedendo ao meu alcence";
		mes "meus poderes é a única chance";
		mes "eventualmente de fazer isso.";
		next;
		set SOUL_Q,4;
		if(checkquest(6008) == -1) changequest 6007,6008;
		mes "[Espírito de Monge]";
		mes "Eu imploro a você...";
		mes "Eu preciso de sua ajuda";
		mes "para que eu possa realizar";
		mes "o verdadeiro potencial dos";
		mes "Monges de hoje.";
		close;
	}
	mes "[Espírito de Monge]";
	mes "...";
	close;
}

job_soul,30,25,7	script	Sage Spirit#link5	754,{
	if (SOUL_Q == 2) {
		mes "[Espírito de Sábio]";
		mes "Fale com o Maia.";
		mes "Tenho medo de que eu possa";
		mes "me confundir, Maia";
		mes "ainda não te explicou";
		mes "a minha situação...";
		close;
	} else if (SOUL_Q > 2) {
		mes "[Espírito de Sábio]";
		mes "Minha busca por sabedorias";
		mes "me concedeu um incrível poder:";
		mes "na vida, eu poderia ter destruido";
		mes "tudo o que eu quisesse. Poucos sábios";
		mes "chegaram ao meu nível...";
		next;
		mes "[Espírito de Sábio]";
		mes "Eu morri, mas eu nunca fui capaz";
		mes "de passar para o próximo mundo.";
		mes "Eu ainda quero usar minhas sabedorias. Eu quero usar meus conhecimentos";
		mes "para eu fazer o que eu quiser,";
		mes "e para destruir o que eu quiser.";
		next;
		mes "[Espírito de Sábio]";
		mes "It is enough if I can";
		mes "lend my power to a Sage";
		mes "that is worthy of receiving";
		mes "it. But to do that, I shall";
		mes "require your help. I beg you,";
		mes "let me become your spirit ally.";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) changequest 6007,6008;
		next;
		mes "[Espírito de Sábio]";
		mes "Eu acredito que você";
		mes "seja o único que possa";
		mes "trazer paz para minha alma";
		mes "que está perturbada...";
		close;
	}
	mes "[Espírito de Sábio]";
	mes "...";
	close;
}

job_soul,25,30,5	script	Alchemist Spirit#link7	744,{
	if (SOUL_Q == 2) {
		mes "[Espírito de Alquimista]";
		mes "Oh! Eu realmente quero";
		mes "falar com você, mas o que";
		mes "eu tenho que dizer não";
		mes "fará muito sentindo ao menos";
		mes "que você fale com Maia primeiro. Mas sim,";
		mes "eu preciso de sua ajuda.";
		close;
	} else if (SOUL_Q > 2) {
		mes "[Espírito de Alquimista]";
		mes "Sem exagero, mas eu era o";
		mes "Alquimista mais rápido da minha";
		mes "época. Na verdade, eu posso ter sido";
		mes "o Alquimista mais rápido até hoje.";
		mes "Mas eu fui arrogante, e";
		mes "me matei em um acidente.";
		next;
		mes "[Espírito de Alquimista]";
		mes "Mas a morte não sufocou";
		mes "minha habilidade. Na verdade, eu";
		mes "aprimoro minha habilidade desde minha";
		mes "morte. Eu não vou para o outro";
		mes "mundo até que eu passe";
		mes "as minhas técnicas...";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) changequest 6007,6008;
		next;
		mes "[Espírito de Alquimista]";
		mes "Sou impotente como espírito,";
		mes "mas com sua ajuda, eu posso";
		mes "influenciar e ajudar os alquimistas";
		mes "de hoje a aprimorarem as suas";
		mes "habilidades. Eu lhe imploro,";
		mes "por favor, me dê essa chance...";
		close;
	}
	mes "[Espírito de Alquimista]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	111,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (!getmapusers("job_soul")) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	stopnpctimer;
}