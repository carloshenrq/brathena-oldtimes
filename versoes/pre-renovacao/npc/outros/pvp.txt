//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                    brAthena Script                     |
// |--------------------------------------------------------|
// | Nome do Script: PvP NPCs                               |
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.4/1.0                                |
// |--------------------------------------------------------|
// | Descrição:                                             |
// | PvP NPCs that are found in the Inns in major cities.   |
// | Includes Yoyo Mode, Nightmare Mode, and Event Mode     |
// |                                                        |
// | Breakdown of function "F_PVPNarrator"                  |
// | arg(0): Map to save player on                          |
// | arg(1): X position on getarg(0) map to save player     |
// | arg(2): Y position on getarg(0) map to save player     |
// |--------------------------------------------------------|
// | Changelog:                                             |
// | 1.0 Script Traduzido[cleyton]                          |
// | 1.1 Script Revisado[cleyton]                           |
// |                                                        |
// \________________________________________________________/

// Morroc
//============================================================
morocc_in,144,138,4	script	PVPNarrator#moc	84,{
	callfunc "F_PVPNarrator","morocc_in",141,139;
}

morocc_in,144,142,4	script	Gate Keeper#moc::PVPGateKeeper	83,{
	mes "[Porteiro]";
	mes "Fico feliz em ser útil.";
	mes "Eu vou abrir o modo de";
	mes "PVP para você. Se você tem alguma";
	mes "dúvida sobre o PVP,";
	mes "modos e regras, por favor,";
	mes "pergunte ao narrador...";
	next;
	switch(select("^FF5533' Modo Pesadelo de PVP'^000000:^3355FF' Modo Yoyo de PVP'^000000:^3355FF'Modo de Evento PVP'^000000:Sair")) {
	case 1:
		mes "[Porteiro]";
		mes "Lamento, mas atualmente o modo Pesadelo não está disponível.";
		mes "Por favor, use o modo Yoyo agora. Nós pedimos desculpas pelo inconveniente.";
		close;
	case 2:
		mes "[Porteiro]";
		mes "A taxa de para entrar é 500 zeny.";
		mes "Você deseja ir para";
		mes "para a arena de luta";
		mes "do modo Yoyo?";
		next;
		switch(select("Entrar:Cancelar")) {
		case 1:
			if (Zeny > 499 && BaseLevel > 30) {
				set zeny,zeny-500;
				warp "pvp_y_room",51,23;
				end;
			}
			else {
				mes "[Porteiro]";
				mes "Desculpe-me, mas";
				mes "você ainda não está preparado?";
				mes "Verifique novamente se você possui os";
				mes "500 zenys solicitados para entrar e";
				mes "você também precisa ter no mínimo level 31!";
				close;
			}
		case 2:
			mes "[Porteiro]";
			mes "Nesse mundo de guerra entre monstros";
			mes "e humanos, essa é uma competição";
			mes "entre humanos. O modo PVP!";
			mes "Isso nos incentiva a nos tornar mais";
			mes "fortes para voltar.";
			mes "Nós damos os parabéns ao seu espírito de competição!";
			close;
		}
	case 3:
		mes "[Porteiro]";
		mes "Bem vindo.";
		mes "Chegue novamente";
		mes "se você possui o bilhete de permissão.";
		next;
		if (countitem(7028) == 0 && countitem(7029) == 0) {
			mes "[Porteiro]";
			mes "O que? Você não tem?";
			mes "Então eu lamento, mas essa arena de batalha é";
			mes "somente para pessoas que tem ";
			mes "os requisitos mínimos ou o cartão de permissão.";
			mes "Você não pode entrar sem ele.";
			close;
		}
		mes "[Porteiro]";
		mes "Certo, muito obrigado por sua participação. Divirta-se!";
		close2;
		warp "pvp_room",51,23;
		end;
	case 4:
		mes "[Porteiro]";
		mes "Nesse mundo de guerra entre monstros";
		mes "e humanos, essa é uma competição";
		mes "entre humanos. O modo PVP!";
		mes "Isso nos incentiva a nos tornar mais";
		mes "fortes para voltar.";
		mes "Nós damos os parabéns ao seu espírito de competição!";
		close;
	}
}

// Alberta
//============================================================
alberta_in,22,146,4	script	PVP Narrator#alb	84,{
	callfunc "F_PVPNarrator","alberta_in",22,148;
}

alberta_in,26,146,4	duplicate(PVPGateKeeper)	Gate Keeper#alb	83

// Prontera
//============================================================
prt_in,56,140,4	script	PVPNarrator#prt	84,{
	callfunc "F_PVPNarrator","prt_in",54,137;
}

prt_in,52,140,4	duplicate(PVPGateKeeper)	Gate Keeper#prt	83

// Geffen
//============================================================
geffen_in,67,63,4	script	PVPNarrator#gef	84,{
	callfunc "F_PVPNarrator","geffen_in",70,59;
}

geffen_in,63,63,4	duplicate(PVPGateKeeper)	Gate Keeper#gef	83

// Payon
//============================================================
payon_in01,142,50,4	script	PVPNarrator#pay	84,{
	callfunc "F_PVPNarrator","payon_in01",142,46;
}

payon_in01,140,53,4	duplicate(PVPGateKeeper)	Gate Keeper#pay	83


// PVP Yoyo Mode Staff
//============================================================
pvp_y_room,86,85,4	script	Fight Square Reception#1	105,{
	set .@mapcount_1,getmapusers("pvp_y_8-1");
	set .@mapcount_2,getmapusers("pvp_y_8-2");
	set .@mapcount_3,getmapusers("pvp_y_8-3");
	set .@mapcount_4,getmapusers("pvp_y_8-4");
	set .@mapcount_5,getmapusers("pvp_y_8-5");
	while(1) {
		switch(select("Prontera [ "+.@mapcount_1+" / 128 ]:Izlude [ "+.@mapcount_2+" / 128 ]:Payon [ "+.@mapcount_3+" / 128 ]:Alberta [ "+.@mapcount_4+" / 128 ]:Morroc [ "+.@mapcount_5+" / 128 ]:Cancel")) {
		case 1:
			callsub S_CheckPVPRoom,@mapcount_1,"pvp_y_8-1";
			break;
		case 2:
			callsub S_CheckPVPRoom,@mapcount_2,"pvp_y_8-2";
			break;
		case 3:
			callsub S_CheckPVPRoom,@mapcount_3,"pvp_y_8-3";
			break;
		case 4:
			callsub S_CheckPVPRoom,@mapcount_4,"pvp_y_8-4";
			break;
		case 5:
			callsub S_CheckPVPRoom,@mapcount_5,"pvp_y_8-5";
			break;
		case 6:
			close;
		}
	}

OnInit:
	waitingroom "Livre para todos",0;
	end;

S_CheckPVPRoom:
	if (getarg(0) >= 128) {
		mes "[Equipe de Recepção da Arena de Luta PVP]";
		mes "Atualmente esse mapa está lotado.";
		next;
		return;
	}
	else {
		warp getarg(1),0,0;
		end;
	}
}

// PVP Nightmare Mode Staff
//============================================================
pvp_n_room,30,85,4	script	Fight Square Reception#2	105,{
	set .@mapcount_1,getmapusers("pvp_n_8-1");
	set .@mapcount_2,getmapusers("pvp_n_8-2");
	set .@mapcount_3,getmapusers("pvp_n_8-3");
	set .@mapcount_4,getmapusers("pvp_n_8-4");
	set .@mapcount_5,getmapusers("pvp_n_8-5");
	while(1) {
		switch(select("Sandwich [ "+.@mapcount_1+" / 64 ]:Lock on [ "+.@mapcount_2+" / 32 ]:Four Room [ "+.@mapcount_3+" / 32 ]:Under cross [ "+.@mapcount_4+" / 32 ]:Compass Room [ "+.@mapcount_5+" / 32 ]:Cancel")) {
		case 1:
			callsub S_CheckPVPRoom,@mapcount_1,"pvp_n_8-1",64;
			break;
		case 2:
			callsub S_CheckPVPRoom,@mapcount_2,"pvp_n_8-2",32;
			break;
		case 3:
			callsub S_CheckPVPRoom,@mapcount_3,"pvp_n_8-3",32;
			break;
		case 4:
			callsub S_CheckPVPRoom,@mapcount_4,"pvp_n_8-4",32;
			break;
		case 5:
			callsub S_CheckPVPRoom,@mapcount_5,"pvp_n_8-5",32;
			break;
		case 6:
			close;
		}
	}

OnInit:
	waitingroom "Livre para todos",0;
	end;

S_CheckPVPRoom:
	if (getarg(0) >= getarg(2)) {
		mes "[Equipe de Recepção da Arena de Luta PVP]";
		mes "Atualmente esse mapa está lotado.";
		next;
		return;
	}
	else {
		warp getarg(1),0,0;
		end;
	}
}

// Function
//============================================================
function	script	F_PVPNarrator	{
	mes "[Guia do PVP]";
	mes "Olá e bem vindo!";
	mes "Eu sou ";
	mes "o responsável pelo modo PVP.";
	mes "Eu sou o narrador do PVP!";
	next;
	switch(select("O que é PVP?:Quais os modos de PVP?:Quais as regras do PVP?:Salvar Posição.:Fim da conversa.")) {
	case 1:
		mes "[Guia do PVP]";
		mes "De uma forma direta";
		mes "o PVP significa";
		mes "'Modo de Jogador contra Jogador '";
		mes "Esse é o único lugar onde as pessoas";
		mes "podem duelar contra os outros.";
		next;
		mes "[Guia do PVP]";
		mes "Diga ao ";
		mes "^3355FFPorteiro^000000";
		mes "que você deseja entrar e ele";
		mes "levará você para a arena de batalha PVP.";
		next;
		mes "[Guia do PVP]";
		mes "Mas, você precisa ter no mínimo";
		mes "o nível 31 e também";
		mes "pagar 500 zeny";
		mes "para poder entrar";
		mes "na arena de PVP.";
		close;
	case 2:
		mes "[Guia do PVP]";
		mes "Você está qualificado.";
		mes "Você pode escolher dois modos.";
		mes "Modo Yoyo, ou ";
		mes "modo Pesadelo.";
		next;
		mes "[Guia do PVP]";
		mes "O modo Yoyo, isenta você de riscos.";
		mes "Você pode ganhar experiência em PVP";
		mes "sem nennhuma restrição ou";
		mes "punição. Esse modo é";
		mes "recomendado para você praticar suas habildades";
		mes "aqui para seguir em frente.";
		next;
		mes "[Guia do PVP]";
		mes "O modo Pesadelo é muito";
		mes "perigoso! Use ele com cuidado.";
		mes "Você vai perder um pouco de";
		mes "sua experiencia real ao ser derrotado.";
		mes "E existe uma pequena chance do";
		mes "seu equipamento cair.";
		next;
		mes "[Guia do PVP]";
		mes "Porém, se você ganhar, as";
		mes "recompensas serão incríveis!";
		mes "Para evitar qualquer mal entendido,";
		mes "você deve pensar";
		mes "duas vezes antes de ir para lá...";
		mes "Boa sorte!";
		close;
	case 3:
		mes "[Guia do PVP]";
		mes "Cada arena de luta";
		mes "tem seu narrador para";
		mes "escolher a sua base";
		mes "de qualificação.";
		next;
		mes "[Guia do PVP]";
		mes "Cada narrador vai pedir";
		mes "para você escolher um dos";
		mes "cinco mapas de PVP para você ir.";
		mes "Escoolha, e entre!";
		next;
		mes "[Guia do PVP]";
		mes "Cada mapa possui uma limitação";
		mes "no número de pessoas que podem";
		mes "participar. Ao participar, você vai ver";
		mes "uma figura no canto que mostra o";
		mes "total de participantes.";
		next;
		mes "[Guia do PVP]";
		mes "Além disso existe o ganho de uma pontuação";
		mes "oculta no modo de PVP. Essa ";
		mes "pontuação será aplicada dentro";
		mes "da zona de PVP, por isso";
		mes "não se preocupe.";
		next;
		mes "[Guia do PVP]";
		mes "Cada jogador começa";
		mes "com 5 pontos de experiência. ";
		mes "Se você vencer, seu";
		mes "ponto vai aumentar em 1 de experiência.";
		next;
		mes "[Guia do PVP]";
		mes "Da mesma forma de que";
		mes "se você for derrotado... Vai perder";
		mes "5 pontos de experiência.";
		mes "Por isso tome cuidado!";
		next;
		mes "[Guia do PVP]";
		mes "Quando você é derrotado";
		mes "e sua experiência é igual";
		mes "ou inferior a 0,";
		mes "você será removido do PVP";
		mes "e seu duelo estará finalizado.";
		next;
		mes "[Guia do PVP]";
		mes "Porém, se sua experência for";
		mes "superior a 0, você ainda pode pedir ajuda";
		mes "para outros jogadores que curem...";
		mes "Você entendeu?";
		next;
		mes "[Guia do PVP]";
		mes "Os comandos de luta dentro";
		mes "da arena de PVP são iguais aos normais.";
		mes "Todos os controles básicos são";
		mes "os mesmos.";
		next;
		mes "[Guia do PVP]";
		mes "Porém, existe mais uma coisa...";
		mes "Entro das arenas de PVP,";
		mes "e nas zonas de luta PVP,";
		mes "você não pode salvar sua posição.";
		mes "Lembre-se bem... Essas regras";
		mes "podem ajudar você garantir sua vitória.";
		close;
	case 4:
		mes "[Guia do PVP]";
		mes "Posição salva...";
		mes "com sucesso.";
		mes "Muito obrigado, Nós";
		mes "esperamos ver você novamente.";
		close2;
		savepoint getarg(0),getarg(1),getarg(2);
		end;
	case 5:
		mes "[Guia do PVP]";
		mes "Nesse mundo de guerra entre monstros";
		mes "e humanos, essa é uma competição";
		mes "entre humanos. O modo PVP!";
		mes "Isso nos incentiva a nos tornar mais";
		mes "fortes para voltar.";
		mes "Nós damos os parabéns ao seu espírito de competição!";
		close;
	}
}

// Event Contestant Entrance
//============================================================
pvp_room,54,85,4	script	Equipe de Registro#1	105,{
	if (countitem(7028) == 0) {
		mes "[Equipe de Registro PVP]";
		mes "O que? Como você chegou aqui?";
		mes "Essa é uma entrada para jogadores apenas.";
		mes "Para maiores detalhes sobre os telespectadores, por favor, inicie o seu registro com a equipe.";
		close;
	}

	if (.pvp_close_n_room == 1 && .pvp_close_s_room == 1 && .pvp_close_w_room == 1 && .pvp_close_e_room == 1) {
		Set .pvp_close_n_room,0;
		Set .pvp_close_s_room,0;
		Set .pvp_close_w_room,0;
		Set .pvp_close_e_room,0;
	}

	if (select("Arena de combate :Cancelar") == 1) {
		mes "[Equipe de Registro PVP]";
		mes "'"+ strcharinfo(0) +"'";
		mes "Are you ready?!";
		next;
		if (select("Sim!:Não!") == 1) {
			mes "[Equipe de Registro PVP]";
			mes "Ok! Eu enviarei você para arena";
			mes "Boa sorte!";
			close2;
			delitem 7028,1; //Invite_For_Duel
			while(1) {
				switch(rand(1,4)) {
				case 1:
					if (.pvp_close_n_room == 0) {
						set .pvp_close_n_room,1;
						warp "pvp_2vs2",40,59;
						end;
					}
				case 2:
					if (.pvp_close_s_room == 0) {
						set .pvp_close_s_room,1;
						warp "pvp_2vs2",59,40;
						end;
					}
				case 3:
					if (.pvp_close_w_room == 0) {
						set .pvp_close_w_room,1;
						warp "pvp_2vs2",20,40;
						end;
					}
				case 4:
					if (.pvp_close_e_room == 0) {
						set .pvp_close_e_room,1;
						warp "pvp_2vs2",40,20;
						end;
					}
				}
			}
		}
		mes "[Equipe de Registro PVP]";
		mes "Volte quando você estiver pronto.";
		close;
	}
	close;

OnInit:
	Set .pvp_close_n_room,0;
	Set .pvp_close_s_room,0;
	Set .pvp_close_w_room,0;
	Set .pvp_close_e_room,0;
	waitingroom "Arena de combate apenas para jogadores",0;
	end;
}

// Spectator Entrance
//============================================================
pvp_room,62,85,4	script	Spectator's Entrance#1::PVPSpectator	105,{
	if (countitem(7029) == 0) {
		mes "[Equipe de Registro para arena PVP]";
		mes "Essa é a entrada para os telespectadores.";
		mes "Para obter mais detalhes sobre a entrada, por favor, registre-se com a equipe ao lado esquerdo.";
		close;
	}

	if (select("Arena de competição:Cancelar") == 1) {
		mes "[Equipe de Registro PVP]";
		mes "Muito bem, obrigado por participar. Divirta-se!";
		close2;
		delitem 7029,1; //Admission_For_Duel
		switch(rand(1,4)) {
		case 1: warp "pvp_2vs2",39,7; end;
		case 2: warp "pvp_2vs2",39,73; end;
		case 3: warp "pvp_2vs2",7,39; end;
		case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	close;

OnInit:
	waitingroom "Entrada para os Telespectadores",0;
	end;
}

pvp_room,70,85,4	duplicate(PVPSpectator)	Spectator's Entrance#2	105

pvp_room,78,85,4	duplicate(PVPSpectator)	Spectator's Entrance#3	105

pvp_room,86,85,4	duplicate(PVPSpectator)	Spectator's Entrance#4	105

// Spectator Warps
//============================================================
pvp_2vs2,5,4,0	script	Equipe Arena de Combate#1::PVPSpecWarp	45,1,1,{
OnTouch:
	mes "[Equipe Arena de batalha]";
	mes "Como posso ajudar?";
	if (select("Banco central para telespectadores.:Deixar a arena de batalha.") == 1) {
		warp "pvp_2vs2",38,38;
		end;
	}
	warp "pvp_room",84,39;
	end;
}

pvp_2vs2,5,74,0	duplicate(PVPSpecWarp)	Equipe Arena de Combate#2	45,1,1

pvp_2vs2,74,74,0	duplicate(PVPSpecWarp)	Equipe Arena de Combate#3	45,1,1

pvp_2vs2,74,5,0	duplicate(PVPSpecWarp)	Equipe Arena de Combate#4	45,1,1

pvp_2vs2,40,40,0	script	Equipe Arena de Combate#5	45,1,1,{
	set mapmove,0;
	mes "[Equipe Arena de batalha]";
	mes "Como posso ajduar?";
	if (select("Banco lateral para telespectadores.:Deixar a arena de batalha.") == 1) {
		switch(rand(1,4)) {
		case 1: warp "pvp_2vs2",39,7; end;
		case 2: warp "pvp_2vs2",39,73; end;
		case 3: warp "pvp_2vs2",7,39; end;
		case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	warp "pvp_c_room",84,39;
	end;
}

// PVP Area Exit warp
//============================================================
pvp_room,51,19,0	script	out#eventpvp	45,4,2,{
OnTouch:
	mes "[Equipe Arena de batalha]";
	mes "Você se divertiu na arena de batalha?";
	mes "Agora para onde você deseja ir?";
	next;
	switch(select("prontera.:morocc.:geffen.:payon.:alberta.:Cancelar.")) {
	case 1: warp "prontera",107,60; end;
	case 2: warp "morocc",157,96; end;
	case 3: warp "geffen",120,36; end;
	case 4: warp "payon",96,100; end;
	case 5: warp "alberta",41,243; end;
	case 6: close;
	}
}
