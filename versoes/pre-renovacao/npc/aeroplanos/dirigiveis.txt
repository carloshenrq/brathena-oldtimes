//  ________________________________________________________
// /                                                        \
// |       _           _   _   _                            |
// |      | |__  _ __ / \ | |_| |__   ___ _ __   __ _       |
// |      | '_ \| '__/ _ \| __| '_ \ / _ \ '_ \ / _` |      |
// |      | |_) | | / ___ \ |_| | | |  __/ | | | (_| |      |
// |      |_.__/|_|/_/   \_\__|_| |_|\___|_| |_|\__,_|      |
// |                                                        |
// |                     brAthena Script                    |
// |--------------------------------------------------------|
// | Nome do Script: Sistema de Aeroplano 	       	  		|
// |--------------------------------------------------------|
// | Criado por: eA/brA Dev. Team                           |
// |--------------------------------------------------------|
// | Versões eA/brA: 1.2/1.0                                |
// |--------------------------------------------------------|
// | Descrição: Sistema e NPCs Globais dos Aeroplanos.	    |
// |--------------------------------------------------------|
// | Changelog:               	                            |
// | 1.0 Script Traduzido [Wolf]				    		|
// | - Anotações		   			  						| 
// \________________________________________________________/

//============================================================ 
//= The Airship System (Juno -> Hugel -> Einbroch -> Lighthalzen -> repeat)
//============================================================ 

airplane,243,74,4	script	#AirshipWarp-1	45,2,2,{
OnTouch:
	if($@airplanelocation == 1)warp "einbroch",90,275;
	if($@airplanelocation == 2)warp "yuno",85,265;
	if($@airplanelocation == 3)warp "lighthalzen",302,75;
	if($@airplanelocation == 4)warp "hugel",182,150;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane,243,29,4	script	#AirshipWarp-2	45,2,2,{
OnTouch:
	if($@airplanelocation == 1)warp "einbroch",90,275;
	if($@airplanelocation == 2)warp "yuno",85,265;
	if($@airplanelocation == 3)warp "lighthalzen",302,75;
	if($@airplanelocation == 4)warp "hugel",182,150;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane,1,1,0	script	EinYuno_Airship	-1,{
OnInit:
while(1)
{
	initnpctimer;
	setnpctimer 0;
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O aeroplano está saindo. O próximo destino é Hugel.",bc_map,0xBA55D3;
	end;
OnTimer15000:
	mapannounce "airplane","Estamos indo para Hugel.",bc_map,0xBA55D3;
	end;
OnTimer30000:
	mapannounce "airplane","Chegaremos em Huguel já mesmo.",bc_map,0xBA55D3;
	end;
OnTimer45000:
	set $@airplanelocation,4;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";	
	mapannounce "airplane","Bem-vindos à Hugel. Tenha um bom passeio.",bc_map,0xBA55D3;
	end;
OnTimer55000:
	mapannounce "airplane","Agora, nós estamos em Hugel. O aeroplano logo sairá.",bc_map,0xBA55D3;
	end;
OnTimer65000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O aeroplano está saindo. O próximo destino é Einbroch.",bc_map,0x00FF00;
	end;
OnTimer80000:
	mapannounce "airplane","Estamos indo para Einbroch.",bc_map,0x00FF00;
	end;
OnTimer95000:
	mapannounce "airplane","Chegaremos em Einbroch já mesmo.",bc_map,0x00FF00;
	end;
OnTimer110000:
	set $@airplanelocation,1;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindos à Einbroch. Tenha um bom passeio.",bc_map,0x00FF00;
	end;
OnTimer120000:
	mapannounce "airplane","Agora, nós estamos em Einbroch. O aeroplano logo sairá.",bc_map,0x00FF00;
	end;
OnTimer130000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O aeroplano está saindo. O próximo destino é Lighthalzen.",bc_map,0xFF8600;
	end;
OnTimer145000:
	mapannounce "airplane","Estamos indo para Lighthalzen.",bc_map,0xFF8600;
	end;
OnTimer160000:
	mapannounce "airplane","Chegaremos em Lighthalzen já mesmo.",bc_map,0xFF8600;
	end;
OnTimer175000:
	set $@airplanelocation,3;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindos à Lighthalzen. Tenham um bom passeio.",bc_map,0xFF8600;
	end;
OnTimer185000:
	mapannounce "airplane","Agora, nós estamos em Lighthalzen. O aeroplano logo sairá.",bc_map,0xFF8600;
	end;
OnTimer195000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	disablenpc "#AirshipWarp-1";
	disablenpc "#AirshipWarp-2";
	mapannounce "airplane","O aeroplano está saindo. O próximo destino é Juno.",bc_map,0x70DBDB;
	end;
OnTimer210000:
	mapannounce "airplane","Estamos indo para Juno.",bc_map,0x70DBDB;
	end;
OnTimer225000:
	mapannounce "airplane","Chegaremos em Juno já mesmo.",bc_map,0x70DBDB;
	end;
OnTimer240000:
	set $@airplanelocation,2;
	enablenpc "#AirshipWarp-1";
	enablenpc "#AirshipWarp-2";
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindos à Juno. Tenha um bom passeio.",bc_map,0x70DBDB;
	end;
OnTimer250000:
	mapannounce "airplane","Agora, nós estamos em Juno. O aeroplano logo sairá.",bc_map,0x70DBDB;
	end;
OnTimer260000:
	stopnpctimer;
}
}

//C4644E

//============================================================ 
//= Some normal NPCS (airplane)
//============================================================ 

airplane,240,64,5	script	Exit#01::Exit2	857,{
	end;
}

airplane,247,64,5	duplicate(Exit2)	Exit#02	857

airplane,240,39,1	duplicate(Exit2)	Exit#03	857

airplane,247,39,1	duplicate(Exit2)	Exit#04	857

airplane,100,69,2	script	Airship Crew#01::Airship Crew	852,{
	mes "[Tripulação]";
	mes "Se você chegou em";
	mes "seu destino e gostaria";
	mes "de descer do avião,";
	mes "por favor, use as escadas";
	mes "à frente. Obrigado";
	mes "pela preferência.";
	close;
}

airplane,250,58,2	script	Airship Staff#airplane	67,{
	if (hg_ma1 == 3) {
		mes "[Tripulação do Aeroplano]";
		mes "Bem-vindo";
		mes "ao Aeroplano.";
		mes "Como posso ajudá-lo?";
		next;
		switch(select("Você tem um passageiro chamado Thierry?")) {
			case 1:
				break;
		}
		mes "[Tripulação do Aeroplano]";
		mes "Sinto muito, mas eu não acho que temos um passageiro com esse nome.";
		close;
	}
	mes "[Tripulação do Aeroplano]";
	mes "Bem-vindo";
	mes "ao Aeroplano.";
	mes "Como posso ajudá-lo?";
	next;
	switch(select("Usando o Aeroplano:Cabine do Capitão:Entretenimentos:Cancelar")) {
	case 1:
		mes "[Tripulação do Aeroplano]";
		mes "Quando você ouvir uma";
		mes "trasmissão anunciando";
		mes "que chegamos ao seu destino,";
		mes "por favor, use uma de nossas";
		mes "saídas localizadas nas extremidades";
		mes "norte e sul do nosso aeroplano.";
		next;
		mes "[Tripulação do Aeroplano]";
		mes "Se você perder sua";
		mes "parada, não se preocupe.";
		mes "O aeroplano está costantemente";
		mes "a caminho e você terá outra";
		mes "chance de chegar ao local";
		mes "que você deseja.";
		break;
	case 2:
		mes "[Tripulação do Aeroplano]";
		mes "A cabine do capitão";
		mes "está localizada na parte";
		mes "frontal do aeroplano. Lá, você";
		mes "pode encontrar o piloto e o capitão";
		mes "do aeroplano.";
		break;
	case 3:
		mes "[Tripulação do Aeroplano]";
		mes "O aeroplano fornece";
		mes "vários Mini-Jogos para";
		mes "o entretenimento de";
		mes "todos os passageiros. Nós";
		mes "convidamos você para tentar sua sorte";
		mes "e habilidades nos Mini-Jogos do aeroplano~";
		break;
	case 4:
		mes "[Tripulação do Aeroplano]";
		mes "Esperamos que você";
		mes "goste de voar em nossos";
		mes "aviões. Obrigado e tenha";
		mes "um bom dia.";
		break;
	}
	close;
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Oh, olá aventureiro.";
	mes "Atualmente, estou em";
	mes "uma jornada sagrada, orando";
	mes "em prol do continente de";
	mes "Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Hum, esse avião vai";
	mes "para Lutie, não? Estou";
	mes "esperando à tempos,";
	mes "e nada de anúncios";
	mes "sobre Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Oh wooow~";
	mes "O céu parece";
	mes "tão bonito e diferente";
	mes "aqui de cima!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	if (hg_ma1 == 3) {
		mes "[Piloto]";
		mes "Queria beber uma cerveja bem gelada.";
		mes "Beber é a meta da minha vida! Beber me dá energia!";
		mes "Eu não sou nada sem bebidas!";
		next;
		mes "[Piloto]";
		mes "Mas! Dirigir sob a influência não é bom.";
		mes "Mas! Que eu queria beber, eu queria.";
		emotion e_sob;
		next;
		switch(select("Você tem um passageiro chamado Thierry?")) {
			case 1:
				break;
		}
		mes "[Piloto]";
		mes "Esse uniforme é";
		mes "muito elegante, mas";
		mes "é muito grosso e desgasta";
		mes "muito dentro do avião.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Ninguém nunca entra";
		mes "nessa sala.";
		mes "E o capitão é um troxa.^FFFFFFEu gostaria de socá-lo.^000000";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Opa!? Quem é!";
		next;
		mes "- ...Ele não está te ouvindo. -";
		close;
	}
	set .@pilot,rand(1,4);
	if (.@pilot == 1) {
		mes "[Piloto]";
		mes "Faz tantooo tempo";
		mes "desde que eu bebi uma";
		mes "boa bebida alcóolica,";
		mes "nesse frio. Mas o";
		mes "trabalho me obriga a ser o";
		mes "mais responsável possível!";
		next;
		mes "[Piloto]";
		mes "Sempre beber com responsabilidade!";
		mes "Mas então, não sei quando foi";
		mes "a última vez em que eu estive de";
		mes "férias ou estive em folga. Pois é, ";
		mes "beber, ver algums programas na TV,";
		mes "essas coisas fazem falta.";
		emotion e_sob;
	} else if (.@pilot == 2) {
		mes "[Piloto]";
		mes "Rapaz, o clima hoje";
		mes "está muito bom.";
		mes "Brilhante, céu aberto,";
		mes "boa visibilidade para";
		mes "voar, está seguro.";
	} else if (.@pilot == 3) {
		mes "[Piloto]";
		mes "Você sabe, o nosso capitão é";
		mes "um cara respeitado. Ele e o irmão";
		mes "são bem conhecidos na";
		mes "indústria aeronáutica.";
		mes "Quem sabe renas foram";
		mes "feitas para bons capitães?";
		next;
		mes "[Piloto]";
		mes "Apenas entre eu e";
		mes "você, eu tenho que te";
		mes "dizer, o Papai Noel usa";
		mes "renas e duendes para";
		mes "trabalhar para você.";
		mes "O homem deve ser um gênio!";
	} else {
		mes "[Piloto]";
		mes "Você sabe, essa coisa";
		mes "toda de pilotagem no ar,";
		mes "é bastante novo, sabe?";
		mes "Sim, eles fizeram essa";
		mes "aviação funcionar tão de pressa.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Ainda assim, eles";
		mes "realmente levaram";
		mes "a sério. Quero dizer, eles tinham-nos";
		mes "de treinamento, enquanto os";
		mes "aviões ainda estavam sendo criados.";
		mes "Não é uma doideira só!?";
	}
	close;
}

//============================================================ 
//= The Airship System (Izlude -> Juno -> Rachel -> Repeat)
//============================================================ 

airplane_01,243,74,4	script	#AirshipWarp-3	45,2,2,{
OnTouch:
	if($@airplanelocation2 == 1)warp "izlude",202,56;
	if($@airplanelocation2 == 2)warp "yuno",20,265;
	if($@airplanelocation2 == 3)warp "ra_fild12",292,204;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane_01,243,29,4	script	#AirshipWarp-4	45,2,2,{
OnTouch:
	if($@airplanelocation2 == 1)warp "izlude",202,56;
	if($@airplanelocation2 == 2)warp "yuno",20,265;
	if($@airplanelocation2 == 3)warp "ra_fild12",292,204;
	end;

OnHide:
	misceffect 16;
	end;
OnUnhide:
	misceffect 215;
	end;
}

airplane_01,1,1,0	script	YunoIzl_Airship	-1,{
OnInit:
while(1)
{
	disablenpc "Airship#airplane02";
	initnpctimer;
	setnpctimer 0;
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","O aeroplano está saindo. O próximo destino é Izlude.",bc_map,0x00FF00;
	end;
OnTimer15000:
	set $@mobinv, $@mobinv+1;
	set $@mobrand, rand(1,3);
	if($@mobinv >= 9 && $@mobrand == 3) {
		stopnpctimer;
		enablenpc "Airship#airplane02";
		donpcevent "Airship#airplane02::OnInvasion";
	}
	mapannounce "airplane_01","Estamos indo para Izlude.",bc_map,0x00FF00;
	end;
OnTimer30000:
	mapannounce "airplane_01","Chegaremos em Izlude já mesmo.",bc_map,0x00FF00;
	end;
OnTimer45000:
	set $@airplanelocation2,1;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";	
	mapannounce "airplane_01","Bem-vindos à Izlude. Tenham um bom passeio.",bc_map,0x00FF00;
	end;
OnTimer55000:
	mapannounce "airplane_01","Agora, nós estamos em Izlude. O aeroplano logo sairá.",bc_map,0x00FF00;
	end;
OnTimer65000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","O avião está saindo. O próximo destino é Juno.",bc_map,0x70DBDB;
	end;
OnTimer80000:
	mapannounce "airplane_01","Estamos indo para Juno.",bc_map,0x70DBDB;
	end;
OnTimer95000:
	mapannounce "airplane_01","Chegaremos em Juno já mesmo.",bc_map,0x70DBDB;
	end;
OnTimer110000:
	set $@airplanelocation2,2;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindos à Juno. Tenham um bom passeio.",bc_map,0x70DBDB;
	end;
OnTimer120000:
	mapannounce "airplane_01","Agora, nós estamos em Juno. O aeroplano logo sairá.",bc_map,0x70DBDB;
	end;
OnTimer130000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	disablenpc "#AirshipWarp-3";
	disablenpc "#AirshipWarp-4";
	mapannounce "airplane_01","O avião está saindo. O próximo destino é Rachel.",bc_map,0xFF8600;
	end;
OnTimer145000:
	mapannounce "airplane_01","Estamos indo para Rachel.",bc_map,0xFF8600;
	end;
OnTimer160000:
	mapannounce "airplane_01","Chegaremos em Rachel já mesmo.",bc_map,0xFF8600;
	end;
OnTimer175000:
	set $@airplanelocation2,3;
	enablenpc "#AirshipWarp-3";
	enablenpc "#AirshipWarp-4";
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindos à Rachel. Tenham um bom passeio.",bc_map,0xFF8600;
	end;
OnTimer185000:
	mapannounce "airplane_01","Agora, nós estamos em Rachel. O aeroplano logo sairá.",bc_map,0xFF8600;
	end;
OnTimer195000:
 	stopnpctimer;
}
OnReturn:
	killmonsterall "airplane_01";
	set $@mobinv, 0;
	mapannounce "airplane_01","Ameaça de monstro invalida. O avião agora está retornando à operação normalmente.",bc_map,0x00FF00;
	startnpctimer;
	end;
}

//============================================================ 
//= Some normal NPCS (airplane_01)
//============================================================ 

airplane_01,240,64,5	duplicate(Exit2)	Exit#05	857

airplane_01,247,64,5	duplicate(Exit2)	Exit#06	857

airplane_01,240,39,1	duplicate(Exit2)	Exit#07	857

airplane_01,247,39,1	duplicate(Exit2)	Exit#08	857

airplane_01,100,69,2	duplicate(Airship Crew)	Airship Crew#02	852

airplane_01,250,58,2	script	Airship Staff#airplane01	67,{
	mes "[Tripulação do Aeroplano]";
	mes "Bem-vindo";
	mes "ao Aeroplano.";
	mes "Como posso ajudá-lo?";
	next;
	switch(select("Usando o Aeroplano:Cabine do Capitão:Entretenimentos:Cancelar")) {
	case 1:
		mes "[Tripulação do Aeroplano]";
		mes "Quando você ouvir uma";
		mes "trasmissão anunciando";
		mes "que chegamos ao seu destino,";
		mes "por favor, use uma de nossas";
		mes "saídas localizadas nas extremidades";
		mes "norte e sul do nosso aeroplano.";
		next;
		mes "[Tripulação do Aeroplano]";
		mes "Se você perder sua";
		mes "parada, não se preocupe.";
		mes "O aeroplano está costantemente";
		mes "a caminho e você terá outra";
		mes "chance de chegar ao local";
		mes "que você deseja.";
		break;
	case 2:
		mes "[Tripulação do Aeroplano]";
		mes "A cabine do capitão";
		mes "está localizada na parte";
		mes "frontal do aeroplano. Lá, você";
		mes "pode encontrar o piloto e o capitão";
		mes "do aeroplano.";
		break;
	case 3:
		mes "[Tripulação do Aeroplano]";
		mes "O aeroplano fornece";
		mes "vários Mini-Jogos para";
		mes "o entretenimento de";
		mes "todos os passageiros. Nós";
		mes "convidamos você para tentar sua sorte";
		mes "e habilidades nos Mini-Jogos do aeroplano~";
		break;
	case 4:
		mes "[Tripulação do Aeroplano]";
		mes "Esperamos que você";
		mes "goste de voar em nossos";
		mes "aviões. Obrigado e tenha";
		mes "um bom dia.";
		break;
	}
	close;
}

airplane_01,50,66,5	script	Apple Merchant#airplane	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto! -";
		mes "- Você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Volte quando estiver -";
		mes "- sem muito peso. -";
		close;
	}
	mes "[Meltz]";
	mes "Bem-vindo à Loja Meltz";
	mes "onde você pode comprar";
	mes "Maçãs ou pode também";
	mes "fazer Sucos de Maçã.";
	next;
	switch(select("Comprar Maçãs.:Fazer Sucos de Maçã.:Cancelar.")) {
	case 1:
		mes "[Meltz]";
		mes "Por favor, fale a quantidade";
		mes "de Maçãs que você deseja";
		mes "comprar. Cada Maçã é 15 zenys";
		mes "e você pode comprar um máximo";
		mes "de 500 Maçãs por vez. Caso não queira,";
		mes "digite '0' para cancelar.";
		next;
		while(1) {
			input .@input,0,501;
			set .@pay,.@input*15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Obrigado por visitar";
				mes "minha loja. Adeus!";
				mes "Venha aqui caso você";
				mes "queria comprar ou";
				mes "comer uma maçã ~";
				close;
			}
			else if (.@input < 0 && .@input > 500) {
				mes "[Meltz]";
				mes "Você digitou um número";
				mes "maior que o valor máximo";
				mes "de 500. Insira novamente";
				mes "os números de maçãs que";
				mes "você deseja comprar novamente.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "vai custar-lhe ^FF0000" + .@pay + "^000000 zenys.";
				mes "Gostaria de continuar?";
				next;
				if (select("Sim:Não") == 1) break;
				mes "[Meltz]";
				mes "Obrigado por visitar";
				mes "minha loja. Adeus!";
				mes "Venha aqui caso você";
				mes "queria comprar ou";
				mes "comer uma maçã ~";
				close;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Sinto muito, mas você";
			mes "não tem zeny suficiente.";
			mes "Verifique seus zenys ou";
			mes "compre menos maçãs.";
			close;
		} else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, eu acho que você não";
			mes "tem espaço suficiente no";
			mes "inventório para maçãs. Libere";
			mes "espaço em seu inventório e depois";
			mes "volte.";
			close;
		} else {
			set zeny,zeny-.@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Obrigado por visitar";
			mes "minha loja. Espero que você";
			mes "aprecie o sabor dessas maçãs~!";
			close;
		}
		break;
	case 2:
		mes "[Meltz]";
		mes "Ok, eu vou precisar de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Maçã para você.";
		mes "Gostaria de continuar?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			mes "[Meltz]";
			if ((countitem(512) < 3) || (countitem(713) < 1)) {
				mes "Me desculpe, mas você não";
				mes "tem os itens suficientes";
				mes "para criar o Suco de Maçã.";
				mes "Novamente, eu preciso de 3 Maçãs";
				mes "e 1 Garrafa Vazia para fazer.";
				close;
			} else {
				mes "Obrigado, por favor aguarde.";
				next;
				mes "^3355FF*Moendo* *Moendo*";
				mes "*Moendo* *Moendo*";
				mes "*Som de Garrafa...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa_Vazia
				getitem 531,1; //Suco_de_Maçã
				mes "[Meltz]";
				mes "Lá vai você~";
				mes "Por favor, volte qualquer dia.";
				close;
			}
			break;
		case 2:
			mes "[Meltz]";
			mes "Obrigado por visitar";
			mes "minha loja. Adeus!";
			mes "Venha aqui caso você";
			mes "queria comprar ou";
			mes "comer uma maçã ~";
			close;
		}
		break;
	case 3:
		mes "[Meltz]";
		mes "Obrigado por visitar";
		mes "minha loja. Adeus!";
		mes "Venha aqui caso você";
		mes "queria comprar ou";
		mes "comer uma maçã ~";
		break;
	}
	close;
}

airplane_01,32,61,4	script	Nils#ein	49,{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables
loopback:
	mes "[Nils]";
	mes "Bem-vindo ao";
	mes "^FF0000Desafio do Ragnarok de Digitação^000000.";
	mes "Gostaria de jogar esse jogo";
	mes "de velocidade de digitação?";
	next;
	menu "Jogar o ^FF0000Desafio do Ragnarok de Digitação^000000",s_Play,"Informações",s_Info,"Ver os recordes",s_Record,"Cancelar",-;

	mes "[Nils]";
	mes "Sinta-se livre para";
	mes "participar do jogo à";
	mes "qualquer momento. Eu vou estar aqui~";
	close;

s_Play:
	mes "[Nils]";
	mes "Ok, temos um novo";
	mes "desafiante!";
	mes "Digite o texto a seguir";
	mes "o mais rápido que puder,";
	mes "sem errar.";
	mes "Vamos então começar!";
	next;
	//below arrays are for simplified entering of new lines ;P

	//first lines array
	setarray $@textstringsa$[0],
	"^00FFFFthkelfkskeldmsiejdlsle^0000FFhfndkelsheidl",
	"^993366hfjdkeldjsieldjs^663366hfjdjeiskdlefvbd",
	"^00FFFFCoboman no chikara-yumei na",
	"^993366belief love luck grimace sweat rush",
	"^00FFFFcallipygian salacious lascivious",
	"^663366uNflAPPaBLe LoVaBLe SeCreTs AnD",
	"^00CCFFburrdingdingdilidingdingphoohudaamb",
	"^FF33CCbarapaphurarlandreamduranbatuhi^990066wooi",
	"^FF33CCLiGhTsPeEd RiGhT SPEed leFT TURn",
	"^00CCFFI'm the King of all Weirdos! Now",
	"^00CCFFBy the power of^000000",
	"^00CCFF...silence. quiet benevolence...";

	//second lines array
	setarray $@textstringsb$[0],"skemd",
	"",
	"chikara-dalookii na chikara da ze!",
	"folktale rodimus optimus bumblebee",
	"licentious prurient concupiscent",
	"BoWLiNg aGaINST tHe KarMA of YoUtH",
	"andoora^0000FFbambarambambamburanbamding",
	"kabamturubamdingding",
	"RiGhT BuRn OrIGInAL GaNgSteR SmACk",
	"you know of my true power. Obey~!",
	"p-po-poi-po-poi-poin-poing",
	"soul mate... wonder. enigma...";
	//for the few 3-liners... =X

	setarray $@textstringsc$[0],"",
	"",
	"COBO ON!",
	"",
	"",
	"",
	"",
	"",
	"",
	"",
	"GOD-POING. I NEVER LOSE!",
	"cloud.";

	//comparisation strings
	setarray $@compstring$[0],
	"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
	"hfjdkeldjsieldjshfjdjeiskdlefvbd",
	"Coboman no chikara-yumei na chikara-dalookii na chikara da ze! COBO ON!",
	"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
	"callipygian salacious lascivious licentious prurient concupiscent",
	"uNflAPPaBLe LoVaBLe SeCreTs AnD BoWLiNg aGaINST tHe KarMA of YoUtH",
	"burrdindingdilidingdingphoohudaambandoorabambarambambamburanbamding",
	"barapaphurarlandreamduranbatuhiwooikabamturubamdingding",
	"LiGhTsPeEd RiGhT SPEed leFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
	"I'm the King of all Weirdos! Now you know of my true power. Obey~!",
	"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
	"...silence. quiet benevolence... soul mate... wonder. enigma... cloud";

	//lengths
	setarray $@lengths[0],20,32,73,73,65,66,67,55,67,66,67,69;

	//	initnpctimer;
	// pick 2 distinct phrases and initialize related variables
	set @index,rand(1,getarraysize($@textstringsa$))-1;
	set @string1a$,$@textstringsa$[@index];
	set @string1b$,$@textstringsb$[@index];
	set @string1c$,$@textstringsc$[@index];
	set @typingstring1$,$@compstring$[@index];

	set @index2,rand(1,getarraysize($@textstringsa$))-1;
	while (@index2 == @index) {
		set @index2,rand(1,getarraysize($@textstringsa$))-1;
	}
	set @string2a$,$@textstringsa$[@index2];
	set @string2b$,$@textstringsb$[@index2];
	set @string2c$,$@textstringsc$[@index2];
	set @typingstring2$,$@compstring$[@index2];

	set @typingkeys,$@lengths[@index]+$@lengths[@index2];
	set @start,gettime(3)*60*60+gettime(2)*60+gettime(1);

	mes "[Nils]";
	mes @string1a$;
	if(@string1b$ != "")mes @string1b$;
	if(@string1c$ != "")mes @string1c$;
	input @comparisonvar$[1];
	next;
	mes "^000000[Nils]";
	mes @string2a$;
	if(@string2b$ != "")mes @string2b$;
	if(@string2c$ != "")mes @string2c$;
	input @comparisonvar$[2];
	next;
	if(@comparisonvar$[1] != @typingstring1$ || @comparisonvar$[2] != @typingstring2$) goto s_Fail;
	set @time,(gettime(3)*60*60+gettime(2)*60+gettime(1))-@start; //seconds taken
	set @mypoints,(@typingkeys * 100) / @time;
	mes "[Nils]";
	mes "Você levou";
	mes "was ^FF0000" + @time + " segundos^000000";
	mes "e sua pontuação foi de";
	mes "^0000FF" + @mypoints + "^000000 pontos";
	next;
	if(@mypoints <= $TypingRecord)goto loopback;
	mes "[Nils]";
	mes "Parabéns!";
	mes "É o novo recorde.";
	mes "Vou te colocar no topo-";
	mes "da lista de recordes.";
	set $TypingRecord,@mypoints;
	set $TypingRecord$,strcharinfo(0);
	close;

s_Fail:
	mes "^000000[Nils]";
	mes "Lamento, mas você não";
	mes "digitou o texto corretamente.";
	mes "Mas não se esqueça, a pratica";
	mes "faz a perfeição!";
	close;

s_Info:
	mes "[Nils]";
	mes "O Desafio do Ragnarok de Digitação";
	mes "é um jogo onde você tem que";
	mes "digitar o texto que nós damos o";
	mes "mais rápido possível. O nome dos melhores";
	mes "ficará gravado para prosteridade. Se você quer";
	mes "fama, não perca essa chance!";
	next;
	mes "[Nils]";
	mes "Só gostaria de dizer";
	mes "que você digitará o texto";
	mes "todo em apenas uma entrada";
	mes "que aparecerá em sua tela";
	mes "de jogo. Portanto, não pressione";
	mes "enter, apenas clique em 'OK'.";
	close;

s_Record:
	mes "[Nils]";
	mes "^0000FF" + $TypingRecord$ + "^000000";
	mes "é o atual";
	mes "recordista com";
	mes "^0000FF" + $TypingRecord + "^000000";
	mes "pontos. Tente bater esse";
	mes "tempo da próxima vez~";
	close;

}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	set .@pilot, rand(1,4);
	if (.@pilot == 1) {
		mes "[Piloto]";
		mes "Longitude, 131 graus leste.";
		mes "Latitude, 37 graus ao norte.";
		mes "Estamos no curso correto, capitão.";
	} else if(.@pilot == 2) {
		mes "[Piloto]";
		mes "Parece que o dia está";
		mes "nebuloso. É sempre difícil";
		mes "navegar quando os céus";
		mes "não estão limpos. Acho que";
		mes "teremos que utilizar o radar.";
	} else if(.@pilot == 3) {
		mes "[Piloto]";
		mes "O capitão é uma boa pessoa";
		mes "e eu não consigo pensar em";
		mes "uma pessoa melhor para comandar";
		mes "esse Aeroplano. Além de sua rígidez";
		mes "é um ótimo motorista.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Ei...!^000000";
		mes "^ff0000Menos conversa^000000";
		mes "^ff0000e mais pilotagem!^000000";
		next;
		mes "[Piloto]";
		mes "Imediatamente, senhor!";
		mes "(Viu o que eu quis dizer?)";
	} else {
		mes "[Piloto]";
		mes "Esse uniforme é";
		mes "muito elegante, mas";
		mes "é muito grosso e desgasta";
		mes "muito dentro do avião.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Quase ninguém entra";
		mes "nessa sala.";
		mes "E o capitão é um troxa.";
		mes "Tenho vontade de socá-lo.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Opa ...! Olá!";
		mes "E-e-está gostando do vôo?!";
	}
	close;
}



airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "É tão estranho!";
	mes "Eu fui visitar o";
	mes "Capitão do Avião e";
	mes "tudo o que vi foi esta";
	mes "estranha rena. Oh!";
	mes "Você acha que...";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "Como esperava, as refeições";
	mes "servidas no vôo são";
	mes "apenas três estrelas.";
	mes "*Descontente* Eu devia ter";
	mes "trago meu chefe para ter";
	mes "desfrutado de boas refeições.";
	close;
}

airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Oi, eu sou Clarice ~";
	mes "Você gostaria de apostar";
	mes "algumas maçãs em um jogo";
	mes "amigável de dados?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

airplane_01,71,31,2	script	Espadachim Shimizu#01	106,{
	mes "[Espadachim Shimizu]";
	mes "Finalmente, após cinco";
	mes "anos de espera...";
	mes "Eu posso ter a minha vingança!";
	next;
	mes "[Espadachim Shimizu]";
	mes "Eu só...";
	mes "Digo que no momento";
	mes "eu não tenho como";
	mes "parar. Mas em breve, em breve,";
	mes "eu terei a minha tão esperada vingança!";
	close;
}

//============================================================ 
//= The Izlude Airship Staff
//============================================================

izlude,201,54,3	script	Tripulação do Aeroplano#izl	91,{
	mes "[Tripulação do Aeroplano]";
	mes "Bem-vindo ao Aeroporto Internacional de Izlude.";
	mes "Como posso ajudá-lo?";
	next;
	if (select("Embarcar no Aeroplano:Cancelar") == 1) {
		mes "[Tripulação do Aeroplano]";
		mes "A passagem custa";
		mes "1,200 zenys, mas se você";
		mes "tiver o Bilhete para Aeroplano,"; 
		mes "essa taxa não é cobrada. Você";
		mes "desejar embarcar agora?";
		next;
		if (select("Sim:Não") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airplane_01",224,64;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airplane_01",224,64;
				close;
			}
			mes "[Tripulação do Aeroplano]";
			mes "Você não tem Zeny suficiente.";
			close;
		}
	}
	mes "[Tripulação do Aeroplano]";
	mes "Obrigado e tenha";
	mes "um bom dia.";
	close;
}

//============================================================ 
//= The Hugel Airship Staff
//============================================================

hugel,182,150,3	script	Tripulação do Aeroplano#hu	91,{
	mes "[Tripulação do Aeroplano]";
	mes "Bem-vindo à República de Schwarzwald.";
	mes "Como posso ajudá-lo?";
	next;
	if (select("Embarcar no Aeroplano:Cancelar") == 1) {
		mes "[Tripulação do Aeroplano]";
		mes "A passagem custa";
		mes "1,200 zenys, mas se você";
		mes "tiver o Bilhete para Aeroplano,"; 
		mes "essa taxa não é cobrada. Você";
		mes "desejar embarcar agora?";
		next;
		if (select("Sim:Não") == 1) {
			if(countitem(7311) > 0) {
				delitem 7311,1;
				warp "airplane",224,64;
				close;
			}
			if(Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airplane",224,64;
				close;
			}
			mes "[Tripulação do Aeroplano]";
			mes "Você não tem Zeny suficiente.";
			close;
		}
	}
	mes "[Tripulação do Aeroplano]";
	mes "Obrigado e tenha";
	mes "um bom dia.";
	close;
}

//============================================================ 
//= The Rachel Airship Staff
//============================================================

ra_fild12,295,208,3	script	Tripulação do Aeroplano#ra	45,1,1,{
OnTouch:
	mes "Para usar o Aeroplano você precisa";
	mes "pagar 1,200 zenys ou utilizar";
	mes "um Bilhete do Aeroplano. Gostaria de usar o serviço?";
	next;
	if (select("Sim:Não") == 1) {
		if (countitem(7311) > 0) {
			delitem 7311,1;
			warp "airplane_01",224,64;
			close;
		}
		if (Zeny >= 1200) {
			set Zeny, Zeny - 1200;
			warp "airplane_01",224,64;
			close;
		}
		mes "Você não tem Zeny suficiente.";
		close;
	}
	mes "Obrigado, e por favor, volte sempre.";
	close;
}

//============================================================ 
//= Juno Airport NPCs
//============================================================

y_airport,144,63,4	script	Tripulação do Aeroplano#Ein	91,{
	mes "[Tripulação do Aeroplano]";
	mes "Bom dia!";
	mes "Gostaria de ir";
	mes "para ^FF0000Einbroch^000000,^FF0000Hugel^000000 ou";
	mes "^FF0000Lighthalzen^000000?";
	next;
	if (select("Sim.:Não.") == 1) {
		mes "[Tripulação do Aeroplano]";
		mes "Obrigado e tenha";
		mes "um bom dia.";
		close2;
		warp "yuno",57,240;
		end;
	}
	mes "[Tripulação do Aeroplano]";
	mes "Obrigado e tenha";
	mes "um bom dia.";
	close;
}

y_airport,141,63,4	script	Tripulação do Aeroplano#Izl	91,{
	mes "[Tripulação do Aeroplano]";
	mes "Bom dia!";
	mes "Gostara de ir";
	mes "para ^FF0000Izlude^000000 ou ^FF0000Rachel^000000?";
	next;
	if (select("Sim.:Não.") == 1) {
		mes "[Tripulação do Aeroplano]";
		mes "Obrigado e tenha";
		mes "um bom dia.";
		close2;
		warp "yuno",50,240;
		end;
	}
	mes "[Tripulação do Aeroplano]";
	mes "Obrigado e tenha";
	mes "um bom dia.";
	close;
}

yuno,14,262,5	script	Tripulação do Aeroplano#yuno01	91,{
	mes "[Tripulação do Aeroplano]";
	mes "Seja bem-vindo ao Aeroporto de Juno.";
	mes "Por favor, utilize essa porta";
	mes "para embarcar no Aeroplano";
	mes "que está sempre voando para Izlude";
	mes "em Rune-Midgard e para";
	mes "Rachel na República de";
	mes "Arunafeltz.";
	next;
	mes "[Tripulação do Aeroplano]";
	mes "Caso contrário, se Juno for";
	mes "o seu destino, por favor,";
	mes "desça essas escadas e";
	mes "fale com a Equipe de Funcionários";
	mes "para te conduzir ao terminal principal. Obrigado,";
	mes "e tenha uma boa viagem.";
	close;
}

yuno,88,263,3	script	Tripulação do Aeroplano#yuno02	91,{
	mes "[Tripulação do Aeroplano]";
	mes "Seja bem-vindo ao Aeroporto de Juno.";
	mes "Por favor, utilize essa porta";
	mes "para embarcar no Aeroplano que";
	mes "está sempre em Einbroch, Lighthalzen";
	mes "e Hugel na República de Schwaltzvalt.";
	next;
	mes "[Tripulação do Aeroplano]";
	mes "Caso contrário, se Juno for";
	mes "o seu destino, por favor,";
	mes "desça essas escadas e";
	mes "fale com a Equipe de Funcionários";
	mes "para te conduzir ao terminal principal. Obrigado,";
	mes "e tenha uma boa viagem.";
	close;
}

//============================================================ 
//= Apple Gambling Function
//============================================================

function	script	applegamble	{

	switch(select("Jogar o Jogo de Dados:Regras do Jogo de Dados:Cancelar")){
	case 3:
		mes "["+getarg(0)+"]";
		mes "Sempre estarei pronto";
		mes "para um jogar um jogo";
		mes "de dados. Volte aqui";
		mes "quando você estiver";
		mes "interessado em jogar, ok?";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "As regras do jogo de dados são";
		mes "bem simples. Primeiro, você";
		mes "aposta uma quantia de Maçãs.";
		mes "Você pode apostar 50";
		mes "Maçãs por vez. Para que fique";
		mes "nas regras, eu só aceito Maçãs como apostas.";
		next;
		mes "["+getarg(0)+"]";
		mes "Mas em, se você tem tanto";
		mes "zeny, e está gastando atoa,";
		mes "vá até o Fruitz e compre";
		mes "quantas maçãs";
		mes "você quiser, hehe~";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, eu começo jogando";
		mes "dois dados de 6 lados.";
		mes "Quando for sua vez, você";
		mes "deverá jogar os dois dados de 6 lados.";
		mes "Depois disso, nós teremos a";
		mes "chance de jogar um terceiro dado.";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora a coisa mais";
		mes "importante. Se você fizer";
		mes "mais que 12, você fracassa,";
		mes "assim perdendo o jogo. Caso contrário,";
		mes "quem fizer mais pontos,";
		mes "é o vencedor. Entendeu?";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, você será o primeiro";
		mes "a decidir se você jogará o";
		mes "terceiro dado ou não. Então,";
		mes "dependendo do resulto, eu jogo";
		mes "meu terceiro dado... Ou simplesmente não.";
		next;
		mes "["+getarg(0)+"]";
		mes "Quando você ganha, você";
		mes "recebe o dobro de Maçãs";
		mes "que você apostou no jogo.";
		mes "Se por acaso houver um empate,";
		mes "é devolvida todas as maçãs que";
		mes "você apostou. Justo, não?";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "Ooh, então você vai jogar";
	mes "comigo? Ótimo! Quantas";
	mes "Maçãs você gostaria de apostar?";
	mes "Não se esqueça, você só pode apostar";
	mes "50 Maçãs por vez. Se você não quiser";
	mes "mais jogar, digite o número '0'.";
	next;
L_Input:
	input @amount;
	if(@amount == 0) {
		mes "["+getarg(0)+"]";
		mes "Mudou de idéia?";
		mes "Eu entendo. Bem,";
		mes "espero que você queira jogar depois.";
		close;
	}
	if(@amount > 50) set @amount,50;
	mes "["+getarg(0)+"]";
	mes "Então você está";
	mes "apostando ^FF0000"+@amount+"^000000 Maçãs.";
	mes "Certo?";
	next;
	if(select("Sim:Não")==2){
		mes "["+getarg(0)+"]";
		mes "Hum, algo errado?";
		mes "Tudo bem, digite novamente";
		mes "o número de maçãs que você";
		mes "deseja apostar";
		next;
		goto L_Input;
	}
	if(countitem(512)<@amount){
		//more apples then in inventory
		//-Improvised-
		mes "["+getarg(0)+"]";
		mes "Ahh...";
		mes "Você não tem Maçãs";
		mes "suficientes com você,";
		mes "né?";
		close;
	}
	delitem 512,@amount;
	mes "["+getarg(0)+"]";
	mes "Ótimo!";
	mes "Vamos começar";
	mes "a jogar! Eu começo jogando~";
	next;
	mes "^0000FF*Rolando - Balançando*";
	set @table1,rand(1,6);
	set @table2,rand(1,6);
	set @tablesub,@table1+@table2;
	next;
	mes "Eu rolei um "+@table1+" e um "+@table2+",";
	mes "e deu um total de "+@tablesub+".";
	mes "Agora é a sua vez,";
	mes strcharinfo(0)+".";
	next;
	menu "Rodar o dado.",-;

	mes "^0000FF*Rolando - Balançando*";
	set @player1,rand(1,6);
	set @player2,rand(1,6);
	set @playersub,@player1+@player2;
	next;
	mes "["+getarg(0)+"]";
	mes strcharinfo(0)+",";
	mes "Você rolou um "+@player1+" e um "+@player2+",";
	mes "e deu um total de ^FF0000"+@playersub+"^000000.";
	next;
	mes "["+getarg(0)+"]";
	if(@playersub == @tablesub) {
		mes "Bem, bem, bem.";
		mes "Nós dois empatamos em um";
		mes "total de "+@playersub+". Bem, a bola";
		mes "está com você. Você vai";
		mes "jogar o terceiro dado,";
		mes strcharinfo(0)+"?";
	} else if(@playersub > @tablesub) {
		mes "Eu fiz apenas ^0000FF"+@tablesub+"^000000,";
		mes "e você está com vantages";
		mes "com seus ^FF0000"+@playersub+"^000000. Você";
		mes "quer jogar mais uma vez?";
		mes "Não se esqueça que, se você fizer";
		mes "mais que 12 pontos você perderá.";
	} else if(@tablesub > @playersub) {
		mes "Meu total foi de ^0000FF"+@tablesub+"^000000,";
		mes "E eu estou com vantagem";
		mes "em cima dos seus^FF0000"+@playersub+"^000000 pontos.";
		mes "Você quer jogar mais uma vez?";
		mes "Não se esqueça que, se você fizer";
		mes "mais que 12 pontos você perderá.";
	}
	next;
	if(select("Jogar denovo.:Não jogar.")==2){
		mes "["+getarg(0)+"]";
		mes "Não vai jogar, heim?";
		if(@tablesub == @playersub) {
			mes "Torça para que eu tire";
			mes "um número bem grande, ou";
			mes "então eu farei mais pontos do que você!";
			mes "Ok, lá vai...";
			next;
			goto L_Table3;
		} else if(@tablesub < @playersub) {
			next;
			mes "["+getarg(0)+"]";
			mes "Tudo bem, eu vejo que";
			mes "você não quer correr o risco";
			mes "de tirar mais que 12.";
			mes "Eu vou jogar então.";
			next;
			goto L_Table3;
		} else if(@tablesub > @playersub) {
		//you have a lower sub total then table, and do not roll 3th -Improvised-
			mes "["+getarg(0)+"]";
			mes "Não vai jogar, heim?";
			mes "Bem, eu não vou";
			mes "jogar também.";
			next;
			mes "["+getarg(0)+"]";
			mes "Então eu tenho";
			mes "um total de^0000FF"+@tablesub+"^000000";
			mes "que é mais que os seus ^FF0000"+@playersub+"^000000.";
			mes "Me desculpe, mas você perdeu";
			mes "o jogo, "+strcharinfo(0)+". Boa";
			mes "sorte da próxima vez.";
		}
		close;
	}
	mes "^0000FF*Rolando - Balançando*";
	set @player3,rand(1,6);
	set @playersub,@playersub+@player3;
	if(@playersub > 12) {
	//player bust --Improvised--
		next;
		mes "["+getarg(0)+"]";
		mes "Nossa... Você rolou um ^FF000"+@player3+"^000000,";
		mes "criando um total de ^FF0000"+@playersub+"^000000.";
		mes "Mesmo assim, é mais do que 12, portanto já era.";
		mes "Desculpe, mas você perdeu o";
		mes "jogo, "+strcharinfo(0)+".";
		close;
	}
	next;
	mes "["+getarg(0)+"]";
	mes "Ei! Você tirou";
	if(@playersub >= @tablesub) {
		mes "um ^FF0000"+@player3+"^000000, dando um total";
		mes "de ^FF0000"+@playersub+"^000000. Agora, se eu não";
		mes "jogar, eu vou perder com certeza!";
		mes "Então, lá vai...";
		next;
		goto L_Table3;
	} else if(@playersub < @tablesub) {
		//player's sub together with 3th die is still too low
		//--Improvised--
		mes "um ^FF0000"+@player3+"^000000, dando um";
		mes "total de ^FF0000"+@playersub+"^000000.";
		mes "Isso não é suficiente para";
		mes "bater meus ^0000FF"+@tablesub+"^000000.";
		mes "Me desculpe, mas você perdeu";
		mes "o jogo, "+strcharinfo(0)+". Vamos parar";
		mes "um pouco, eu suponho...";
		close;
	}
L_Table3:
	mes "^0000FF*Rolando - Balançando*";
	set @table3,rand(1,6);
	set @tablesub,@tablesub+@table3;
	next;
	if(@tablesub > 12) {
		mes "["+getarg(0)+"]";
		mes "Eita? Eu tirei um ^0000FF"+@table3+"^000000, criando";
		mes "um total de ^0000FF"+@tablesub+"^000000. Odeio dizer";
		mes "isso, mas eu perdi.";
		mes "Pegue seus prêmios antes";
		mes "que eu chore, "+strcharinfo(0)+"~!";
		close2;
		getitem 512,@amount*2;
		end;
	}
	mes "["+getarg(0)+"]";
	if(@tablesub < @playersub) {
		mes "Eu tirei um "+@table3+", which";
		mes "dando um total de ^0000FF"+@tablesub+"^000000.";
		mes "Mas... Isso não é suficiente";
		mes "para bater seus ^FF0000"+@playersub+"^000000. Parece";
		mes "que não consigo jogar";
		mes "com você, "+strcharinfo(0)+"...";
		close2;
		getitem 512,@amount*2;
		end;
	} else if(@tablesub > @playersub) {
		mes "Eu tirei um ^0000FF"+@table3+"^000000, me dando";
		mes "um total de ^0000FF"+@tablesub+"^000000 que";
		mes "bate o seu total de ^FF0000"+@playersub+"^000000.";
		mes "Me desculpe, mas você perdeu o";
		mes "jogo, "+strcharinfo(0)+". Vamos";
		mes "parar um pouco, eu suponho...";
		close;
	} else if(@tablesub == @playersub) {
		//Result = tie, --Improvised--
		mes "Eu tirei um ^0000FF"+@table3+"^000000, me dando";
		mes "um total de ^0000FF"+@tablesub+"^000000, dando";
		mes "o mesmo total que o seu.";
		mes "Bem, esse jogo não teve um";
		mes "ganhador nem perdedor, "+strcharinfo(0)+".";
		close2;
		getitem 512,@amount;
		end;
	}
}